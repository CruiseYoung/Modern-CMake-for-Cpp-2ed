对于CMake项目，工具链包括用于构建和运行应用程序的所有工具——工作环境、生成器、CMake可执行文件，以及编译器。

当构建因为一些神秘的编译和语法错误而停止时，一个经验较少的用户会感到多么困惑。他们必须深入源代码，试图理解发生了什么。

经过一个小时的调试，发现正确的解决方案是更新编译器。

我们能否为用户提供更好的体验，并在开始构建之前检查编译器是否支持所有必需的功能？当然可以！我们有方法指定这些要求。如果工具链不支持所有必需的功能，CMake将提前停止并显示清晰的错误消息，要求用户介入。

\mySubsubsection{4.6.1.}{设置C++标准}

我们可能考虑的第一个步骤是，指定编译器应支持的所需C++标准，以构建我们的项目。对于新项目，建议设置C++14作为最低标准，但最好是C++17或C++20。从CMake 3.20开始，如果编译器支持，可以设置所需的标准为C++23。此外，从CMake 3.25开始，还有一个选项可以将标准设置为C++26，尽管这目前只是一个占位符。

\begin{myNotic}{Note}
自C++11正式发布以来已经超过10年，它不再是现代C++标准。除非目标环境非常老旧，否则不建议以这个版本开始项目
\end{myNotic}

另一个坚持旧标准的原因可能是，如果正在构建难以升级的遗留目标。然而，C++委员会非常努力地保持C++向后兼容，通常将标准升级到更高版本都不会有问题。

CMake支持按目标逐一设置标准（这对于代码库的某些部分确实非常古老的部分很有用），但最好在整个项目中统一标准。这可以通过将CMAKE\_CXX\_STANDARD变量设置为以下值来实现：98、11、14、17、20、23或26，例如：

\begin{cmake}
set(CMAKE_CXX_STANDARD 23)
\end{cmake}

这将作为随后定义的所有目标的默认值（最好将其设置在根列表文件的上方）。如果需要，可以对每个目标进行重写：

\begin{shell}
set_property(TARGET <target> PROPERTY CXX_STANDARD <version>)
\end{shell}

或者：

\begin{shell}
set_target_properties(<targets> PROPERTIES CXX_STANDARD <version>)
\end{shell}

第二种方式允许在需要时指定多个目标。

\mySubsubsection{4.6.2.}{坚持标准支持}

前一部分提到的CXX\_STANDARD属性不会阻止CMake继续构建，即使编译器不支持所需的版本——视为一个偏好。CMake不知道代码是否用了，以前编译器中不可用的那些新特性。

如果我们确信这将不会成功，可以设置另一个变量（以与前一个相同的方式在每个目标上可重写），明确要求我们针对的标准：

\begin{cmake}
set(CMAKE_CXX_STANDARD_REQUIRED ON)
\end{cmake}

如果系统中的编译器不支持所需的标准，用户将看到以下消息，并且构建将停止：

\begin{shell}
Target "Standard" requires the language dialect "CXX23" (with compiler extensions), but CMake does not know the compile flags to use to enable it.
\end{shell}

要求C++23可能有点过，即使对于现代环境也是如此。但是C++20对于最新的系统来说应该可以，因为自从2021/2022年以来，GCC/Clang/MSVC普遍都支持。

\mySubsubsection{4.6.3.}{特定供应商的扩展}

根据组织中实施的政策，可能会对允许或禁用特定供应商的扩展感兴趣。这些是什么呢？可以说C++标准对于某些编译器生产商的需求来说进展缓慢，所以他们决定为语言添加他们自己的功能——可以称之为扩展。例如，C++技术报告1（TR1）是一个库扩展，引入了正则表达式、智能指针、哈希表和随机数生成器，这些在成为常见功能之前就已经存在。为了支持GNU项目发布的此类插件，CMake将标准编译器标志(-std=c++14)替换为-std=gnu++14。

一方面，其允许一些方便的功能。另一方面，代码将失去可移植性，因为在切换到不同的编译器（或者用户这样做时）可能无法构建！这也是一个按目标设置的属性，其中有一个默认变量，CMAKE\_CXX\_EXTENSIONS。CMake在这方面更加宽松，默认允许扩展，除非明确告诉它不要这样做：

\begin{cmake}
set(CMAKE_CXX_EXTENSIONS OFF)
\end{cmake}

如果可能的话，我建议这样做，因为这将坚持使用与供应商无关的代码。这样的代码不会给用户带来不必要的限制。类似于之前的选项，可以使用set\_property()在每个目标上更改这个值。

\mySubsubsection{4.6.4.}{过程间优化}

编译器在单个翻译单元级别优化代码，所以.cpp文件将进行预处理、编译，然后优化。在这些操作中生成的中间文件，然后传递给链接器，以创建单个二进制文件。然而，现代编译器在链接时，具有执行跨过程优化的能力，也称为链接时优化。这允许所有编译单元作为一个统一的模块进行优化，原则上会得到更好的结果（有时以构建速度更慢和内存消耗更多为代价）。

如果编译器支持过程间优化，那就使用它。我们将遵循相同的方法，负责此设置的变量称为CMAKE\_INTERPROCEDURAL\_OPTIMIZATION。但在设置它之前，需要确保编译器是否支持：

\begin{cmake}
include(CheckIPOSupported)
check_ipo_supported(RESULT ipo_supported)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ${ipo_supported})
\end{cmake}

需要包含一个内置模块才能访问check\_ipo\_supported()命令。如果优化不支持，这段代码将优雅地失败，并回退到默认行为。

\mySubsubsection{4.6.5.}{检查支持的编译器特性}

正如之前讨论的，如果构建失败，最好是在早期失败，这样就可以向用户提供清晰的反馈消息，并缩短等待时间。有时特别关心哪些C++特性支持（哪些不支持），CMake将在配置阶段询问编译器，并将可用特性列表存储在CMAKE\_CXX\_COMPILE\_FEATURES变量中。我们可以编写一个非常具体的检查，并询问是否支持某个特性：

\filename{ch04/07-features/CMakeLists.txt}

\begin{cmake}
list(FIND CMAKE_CXX_COMPILE_FEATURES cxx_variable_templates result)
if(result EQUAL -1)
    message(FATAL_ERROR "Variable templates are required for compilation.")
endif()
\end{cmake}

为每个我们使用的特性编写一个检查是一项艰巨的任务。甚至CMake的作者也建议只检查某些高级元特性是否存在：cxx\_std\_98、cxx\_std\_11、cxx\_std\_14、cxx\_std\_17、cxx\_std\_20、cxx\_std\_23和cxx\_std\_26。每个元特性都表示编译器支持特定的C++标准，可以像之前示例中那样使用。

CMake知道的所有特性的完整列表可以在文档中找到：\url{https://cmake.org/cmake/help/latest/prop_gbl/CMAKE_CXX_KNOWN_FEATURES.html}。

\mySubsubsection{4.6.6.}{编译测试文件}

当我在使用GCC 4.7.x编译应用程序时，发生了一个特别有趣的情况。我已经在编译器的参考中手动确认了所有C++11特性都得到了支持。然而，解决方案仍然无法正确工作。代码默默地忽略了标准头文件的调用。结果发现，这个特定的编译器存在一个bug，正则表达式库没有实现。

没有检查可以避免这类罕见错误（而且你不应该需要检查它们！），但可能会想要使用最新标准的一些尖端实验特性，而又不知道哪些编译器支持。可以通过创建一个使用特殊特性的测试文件，来测试项目是否能够工作，这个文件可以快速编译和执行。

CMake提供了两个配置时间命令，try\_compile()和try\_run()，以验证目标平台上所需的一切是否得到支持。

try\_run()命令会给予更多的自由，因为它可以确保代码不仅编译成功，而且执行也正确（可能想要测试正则表达式是否工作）。当然，这不会在交叉编译场景中工作（因为主机无法运行为不同目标构建的可执行文件），这个检查的目的是向用户提供快速反馈(能正常编译)，所以它不是用来运行单元测试或任何复杂的东西——文件尽可能简单：

\filename{ch04/08-test\_run/main.cpp}

\begin{cpp}
#include <iostream>
int main()
{
    std::cout << "Quick check if things work." << std::endl;
}
\end{cpp}

调用try\_run()并不复杂。首先设置所需的标准，然后调用try\_run()，并将收集的信息输出给用户：

\filename{ch04/08-test\_run/CMakeLists.txt}

\begin{cmake}
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
try_run(run_result compile_result
        ${CMAKE_BINARY_DIR}/test_output
        ${CMAKE_SOURCE_DIR}/main.cpp
        RUN_OUTPUT_VARIABLE output)
message("run_result: ${run_result}")
message("compile_result: ${compile_result}")
message("output:\n" ${output})
\end{cmake}

这个命令看起来挺吓人，但实际上只需要几个参数就可以编译，并运行一个非常基本的测试文件。我还使用了可选的RUN\_OUTPUT\_VARIABLE关键字来收集stdout的输出。

下一步是扩展基本测试文件，使用实际项目中使用的C++特性——比如通过添加一个变长模板，来查看目标机器上的编译器是否能够处理它。

最后，可以在条件块中检查收集的输出是否符合预期，并在出现问题时打印message(SEND\_ERROR)。SEND\_ERROR关键字将允许CMake继续配置阶段，但将阻止生成构建系统。这在显示所有遇到的错误后才终止构建之前非常有用。我们现在知道如何确保编译可以完整完成。

接下来，让我们转向下一个主题，禁用源内构建。
























