我们如何使项目A的目标对消费项目B可用？通常，我们会使用find\_package()命令，但这需要创建一个包并在系统上安装它。虽然这种方法很有用，但它涉及到一些工作。有时候，我们只是需要一个快速构建项目并使其目标对其他项目可用的方法。

一种节省时间的方法是在项目B中包含项目A的主列表文件，该文件已经包含了所有目标定义。然而，这个文件可能还包括全局配置、具有副作用的CMake命令、额外的依赖项，以及B项目可能不需要的目标（比如单元测试）。所以，这不是最好的方法。相反，我们可以为消费项目B提供一个目标导出文件，通过include()命令包含：

\begin{cmake}
cmake_minimum_required(VERSION 3.26.0)
project(B)
include(/path/to/A/TargetsOfA.cmake)
\end{cmake}

这将使用add\_library()和add\_executable()等命令定义A的所有目标，并设置正确的属性。

您必须在TARGETS关键字后指定要导出的所有目标，并在FILE后提供目标文件名。其他参数是可选的：

\begin{shell}
export(TARGETS [target1 [target2 [...]]]
       [NAMESPACE <namespace>] [APPEND] FILE <path>
       [EXPORT_LINK_INTERFACE_LIBRARIES]
)
\end{shell}

以下是各个参数的解释：

\begin{itemize}
\item
NAMESPACE推荐用来指示目标是从其他项目导入的。

\item
APPEND防止CMake在写入前清除文件内容。

\item
EXPORT\_LINK\_INTERFACE\_LIBRARIES导出目标的链接依赖项（包括导入的和特定配置的变体）。
\end{itemize}

让我们将这种导出方法应用到Calc库示例中，该库提供了两个简单的方法：

\filename{ch14/01-export/src/include/calc/basic.h}

\begin{cpp}
#pragma once
int Sum(int a, int b);
int Multiply(int a, int b);
\end{cpp}

我们需要声明Calc目标以便我们有东西可以导出：

\filename{ch14/01-export/src/CMakeLists.txt}

\begin{cmake}
add_library(calc STATIC basic.cpp)
target_include_directories(calc INTERFACE include)
\end{cmake}

然后，为了生成导出文件，我们使用export(TARGETS)命令：

\filename{ch14/01-export/CMakeLists.txt (fragment)}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(ExportCalcCXX)
add_subdirectory(src bin)
set(EXPORT_DIR "${CMAKE_CURRENT_BINARY_DIR}/cmake")
export(TARGETS calc
    FILE "${EXPORT_DIR}/CalcTargets.cmake"
    NAMESPACE Calc::
)
\end{cmake}

我们的导出目标声明文件将位于构建树的cmake子目录中（遵循.cmake文件的约定）。为了避免稍后重复这个路径，我们将其设置在EXPORT\_DIR变量中。然后，我们调用export()来生成目标声明文件CalcTargets.cmake，其中包含calc目标。对于包含这个文件的项目，它将作为Calc::calc可见。

请注意，这个导出文件还不是一个包。更重要的是，这个文件中的所有路径都是绝对路径，并且硬编码到构建树中，这使得它们不可重定位（在“理解与重定位目标相关的问题”部分讨论）。

export()命令还有一个使用EXPORT关键字的简写版本：

\begin{shell}
export(EXPORT <export> [NAMESPACE <namespace>] [FILE <path>])
\end{shell}

然而，它需要一个预定义的导出名称，而不是要导出的目标列表。 <export>这样的实例是由install(TARGETS)创建的目标命名列表（我们将在“安装逻辑目标”部分介绍这个命令）。

以下是一个演示如何在实践中使用这种简写的微小示例：

\filename{ch14/01-export/CMakeLists.txt (continued)}

\begin{cmake}
install(TARGETS calc EXPORT CalcTargets)
export(EXPORT CalcTargets
    FILE "${EXPORT_DIR}/CalcTargets2.cmake"
    NAMESPACE Calc::
)
\end{cmake}

这段代码的工作方式与前面的示例类似，但现在它在export()和install()命令之间共享了同一个目标列表。

两种生成导出文件的方法产生类似的结果。它们包括一些样板代码和定义目标的几行。将<build-tree>设置为构建树路径后，它们将创建一个类似以下的目标导出文件：

\filename{<build-tree>/cmake/CalcTargets.cmake (fragment)}

\begin{cmake}
# Create imported target Calc::calc
add_library(Calc::calc STATIC IMPORTED)
set_target_properties(Calc::calc PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES
    "/<source-tree>/include"
)
# Import target "Calc::calc" for configuration ""
set_property(TARGET Calc::calc APPEND PROPERTY
    IMPORTED_CONFIGURATIONS NOCONFIG
)
set_target_properties(Calc::calc PROPERTIES
    IMPORTED_LINK_INTERFACE_LANGUAGES_NOCONFIG "CXX"
    IMPORTED_LOCATION_NOCONFIG "/<build-tree>/libcalc.a"
)
\end{cmake}

通常，我们不会编辑甚至打开这个文件，但重要的是要注意文件中的路径将是硬编码的（参见突出显示的行）。在当前的形式下，构建的项目是不可重定位的。要改变这一点，需要采取一些额外的步骤。在下一节中，我们将解释什么是重定位以及为什么它很重要。












































































