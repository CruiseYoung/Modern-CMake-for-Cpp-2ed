我们如何使项目A的目标对项目B可用？通常，我们会使用find\_package()命令，但这需要创建一个包并在系统上安装。虽然这种方法有用，但是略显麻烦。有时，我们构建一个项目后，希望有一种简单的方法使得项目中的目标可以被其他项目使用。

一种简单的方法是直接在项目B中包含项目A的主CMakeLists.txt，该文件中包含了所有目标定义。然而，这个文件还会包括全局配置、具有副作用的CMake命令、额外的依赖项，以及B项目可能不需要的目标（比如单元测试）。因此，这种方法并不太好。我们可以使用如下的方式，在项目A中导出一个.cmake文件（称为目标导出文件），然后在项目B中通过include()命令包含该文件，实现该目的：

\begin{cmake}
cmake_minimum_required(VERSION 3.26.0)
project(B)
include(/path/to/A/TargetsOfA.cmake)
\end{cmake}

项目A的目标导出文件中包含使用add\_library()和add\_executable()等命令定义的所有目标，并包含为这些目标设置的属性。

项目A中使用如下命令生成目标导出文件，命令中必须在TARGETS关键字后指定要导出的所有目标，并在FILE后提供目标文件名。其他参数是可选的：

\begin{shell}
export(TARGETS [target1 [target2 [...]]]
       [NAMESPACE <namespace>] [APPEND] FILE <path>
       [EXPORT_LINK_INTERFACE_LIBRARIES]
)
\end{shell}

其中的参数：

\begin{itemize}
\item
NAMESPACE: 目标导出文件中，所有目标前都会包含NAMESPACE指定的前缀，这些目标被导入其他项目使用时，也应该包含该前缀。推荐为导出的目标都指定前缀，以在使用时可以知道目标是从其他项目导入的。

\item
APPEND: 将导出的内容追加到 FILE 指定的文件，避免写入前清除原文件的内容。

\item
EXPORT\_LINK\_INTERFACE\_LIBRARIES: 同时导出目标的链接依赖项（包括导入的和特定配置的变体）。
\end{itemize}

让我们将这种导出方法应用到Calc库示例中，该库提供了两个简单的方法：

\filename{ch14/01-export/src/include/calc/basic.h}

\begin{cpp}
#pragma once
int Sum(int a, int b);
int Multiply(int a, int b);
\end{cpp}

首先需要声明Calc目标以便我们有东西可以导出：

\filename{ch14/01-export/src/CMakeLists.txt}

\begin{cmake}
add_library(calc STATIC basic.cpp)
target_include_directories(calc INTERFACE include)
\end{cmake}

然后，使用export(TARGETS)命令生成导出文件：

\filename{ch14/01-export/CMakeLists.txt (fragment)}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(ExportCalc CXX)
add_subdirectory(src bin)
set(EXPORT_DIR "${CMAKE_CURRENT_BINARY_DIR}/cmake")
export(TARGETS calc
    FILE "${EXPORT_DIR}/CalcTargets.cmake"
    NAMESPACE Calc::
)
\end{cmake}

如果想要将目标声明文件导到构建树的cmake子目录中（遵循.cmake文件的约定），为了能够复用路径，将出目录路径设置在EXPORT\_DIR变量中。然后，调用export()来生成目标声明文件CalcTargets.cmake，其中包含了定义的calc目标。对于使用include包含这个文件的项目，可以直接通过Calc::calc使用目标。

注意，这个导出文件还不是一个包。更重要的是，这个文件中的所有路径都是绝对路径，并且硬编码到构建树中，这使得它们不可重定位。

export()命令还有一个使用EXPORT关键字的简写版本：

\begin{shell}
export(EXPORT <export> [NAMESPACE <namespace>] [FILE <path>])
\end{shell}

然而，它需要一个预定义的导出名称，而不是要导出的目标列表， <export>这样的实例是由install(TARGETS)创建的目标命名列表。

以下是如何在实践中使用这种简写的示例：

\filename{ch14/01-export/CMakeLists.txt (continued)}

\begin{cmake}
install(TARGETS calc EXPORT CalcTargets)
export(EXPORT CalcTargets
    FILE "${EXPORT_DIR}/CalcTargets2.cmake"
    NAMESPACE Calc::
)
\end{cmake}

这段代码的工作方式与前面的示例类似，但现在它在export()和install()命令之间共享了同一个目标列表。

两种生成导出文件的方法产生类似的结果，包括一些样板代码和定义目标的几行。将<build-tree>设置为构建树路径后，将创建一个类似以下的目标导出文件：

\filename{<build-tree>/cmake/CalcTargets.cmake (片段)}

\begin{cmake}
# Create imported target Calc::calc
add_library(Calc::calc STATIC IMPORTED)
set_target_properties(Calc::calc PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES
    "/<source-tree>/include"
)
# Import target "Calc::calc" for configuration ""
set_property(TARGET Calc::calc APPEND PROPERTY
    IMPORTED_CONFIGURATIONS NOCONFIG
)
set_target_properties(Calc::calc PROPERTIES
    IMPORTED_LINK_INTERFACE_LANGUAGES_NOCONFIG "CXX"
    IMPORTED_LOCATION_NOCONFIG "/<build-tree>/libcalc.a"
)
\end{cmake}

通常，我们不会编辑甚至打开这个文件，但重要的是文件中的路径为硬编码的（参见突出显示的行）。在当前的形式下，构建的项目不可重定位。要改变这一点，需要采取一些其他步骤。下一节中，我们将解释什么是重定位，及其重要性。












































































