
程序分析和测试是确保解决方案质量的重要组成部分。例如，在运行测试代码时使用Valgrind会更加有效（因为它的一致性和覆盖范围）。因此，我们会在同一个地方配置测试和程序分析。图15.5展示了设置它们所需的执行流程和文件：

\myGraphic{0.8}{content/chapter15/images/5.png}{图15.5: 用于启用测试和程序分析的文件}

括号中的数字代表列表文件的处理顺序。从顶层的列表文件开始，添加src和test目录：

\begin{itemize}
\item
在src目录中，包含Coverage、Format和CppCheck模块，并添加src/calc和src/calc\_console目录。

\item
在src/calc目录中，定义目标并使用包含的模块进行配置。

\item
在src/calc\_console目录中，定义目标并使用包含的模块进行配置。

\item
在test目录中，包含Testing（其中包含了Memcheck）并添加test/calc和test/calc\_console目录。

\item
在test/calc目录中，定义测试目标并使用包含的模块进行配置。

\item
在test/calc\_console目录中，定义测试目标并使用包含的模块进行配置。
\end{itemize}

让我们检查test目录下的列表文件：

\filename{ch15/01-full-project/test/CMakeLists.txt}

\begin{cmake}
include(Testing)
add_subdirectory(calc)
add_subdirectory(calc_console)
\end{cmake}

在这个级别上，包含Testing实用程序模块以提供来自calc和calc\_console目录的目标组的功能：

\filename{ch15/01-full-project/cmake/Testing.cmake (fragment)}

\begin{cmake}
include(FetchContent)
FetchContent_Declare(
    googletest
    GIT_REPOSITORY https://github.com/google/googletest.git
    GIT_TAG v1.14.0
)
# For Windows: Prevent overriding the parent project's
# compiler/linker settings
set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
option(INSTALL_GMOCK "Install GMock" OFF)
option(INSTALL_GTEST "Install GTest" OFF)
FetchContent_MakeAvailable(googletest)

# ...
\end{cmake}

我们启用了测试，并包含了FetchContent模块以获取GTest和GMock。虽然GMock在这个项目中没有使用，但它与GTest在同一仓库中，所以我们同样配置它。关键的配置步骤是通过使用option()命令防止这些框架的安装影响我们的项目安装。

在同一文件中，我们定义了一个AddTests()函数来方便地全面测试业务目标：

\filename{ch15/01-full-project/cmake/Testing.cmake (continued)}

\begin{cmake}
# ...
include(GoogleTest)
include(Coverage)
include(Memcheck)
macro(AddTests target)
    message("Adding tests to ${target}")
    target_link_libraries(${target} PRIVATE gtest_main gmock)
    gtest_discover_tests(${target})
    AddCoverage(${target})
    AddMemcheck(${target})
endmacro()
\end{cmake}

首先，我们包含了必要的模块：GoogleTest与CMake捆绑在一起，而Coverage和Memcheck是我们项目中包含的自定义实用程序模块。然后提供了AddTests宏来准备目标进行测试、应用覆盖率度量以及内存检查。AddCoverage()和AddMemcheck()函数分别定义在它们各自的实用程序模块中。现在，我们可以继续实现它们。

\mySubsubsection{15.5.1.}{准备Coverage模块}

在各种目标上添加覆盖率涉及几个步骤。Coverage模块提供了一个函数来为指定的目标定义覆盖率目标：

\filename{ch15/01-full-project/cmake/Coverage.cmake (片段)}

\begin{cmake}
function(AddCoverage target)
    find_program(LCOV_PATH lcov REQUIRED)
    find_program(GENHTML_PATH genhtml REQUIRED)
    add_custom_target(coverage-${target}
        COMMAND ${LCOV_PATH} -d . --zerocounters
        COMMAND $<TARGET_FILE:${target}>
        COMMAND ${LCOV_PATH} -d . --capture -o coverage.info
        COMMAND ${LCOV_PATH} -r coverage.info '/usr/include/*'
            -o filtered.info
        COMMAND ${GENHTML_PATH} -o coverage-${target}
            filtered.info --legend
        COMMAND rm -rf coverage.info filtered.info
        WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
    )
endfunction()

# ...
\end{cmake}

此实现与第11章《测试框架》中介绍的略有不同，因为它现在包括目标名称作为输出路径的一部分，以防止名称冲突。接下来，我们需要一个函数来清除之前的覆盖率结果：

\filename{ch15/01-full-project/cmake/Coverage.cmake (续)}

\begin{cmake}
# ...

function(CleanCoverage target)
    add_custom_command(TARGET ${target} PRE_BUILD COMMAND
        find ${CMAKE_BINARY_DIR} -type f
        -name '*.gcda' -exec rm {} +)
endfunction()

# ...
\end{cmake}

此外，我们还有一个函数来准备目标以进行覆盖率分析：

\filename{ch15/01-full-project/cmake/Coverage.cmake (片段)}

\begin{cmake}
# ...
function(InstrumentForCoverage target)
    if (CMAKE_BUILD_TYPE STREQUAL Debug)
        target_compile_options(${target}
                               PRIVATE --coverage -fno-inline)
        target_link_options(${target} PUBLIC --coverage)
    endif()
endfunction()
\end{cmake}

InstrumentForCoverage()函数应用于src/calc和src/calc\_console，使目标calc\_obj和calc\_console\_static在执行时能够生成覆盖率数据文件。

为了生成两个测试目标的报告，在使用Debug构建类型配置项目后，执行以下cmake命令：

\begin{shell}
cmake --build <build-tree> -t coverage-calc_test
cmake --build <build-tree> -t coverage-calc_console_test
\end{shell}

接下来，我们希望对我们定义的多个目标进行动态程序分析，所以为了应用在第12章《程序分析工具》中介绍的Memcheck模块，我们需要稍微调整它以扫描多个目标。

\mySubsubsection{15.5.2.}{准备Memcheck模块}

由AddTests()发起的Valgrind内存管理报告的生成从Memcheck模块开始，我们从它的初始设置开始：

\filename{ch15/01-full-project/cmake/Memcheck.cmake (fragment)}

\begin{cmake}
include(FetchContent)
FetchContent_Declare(
    memcheck-cover
    GIT_REPOSITORY https://github.com/Farigh/memcheck-cover.git
    GIT_TAG release-1.2
)
FetchContent_MakeAvailable(memcheck-cover)
\end{cmake}

这部分代码我们已经很熟悉了。现在，让我们看看创建生成报告所需目标的函数：

\filename{ch15/01-full-project/cmake/Memcheck.cmake (续)}

\begin{cmake}
function(AddMemcheck target)
    set(MEMCHECK_PATH ${memcheck-cover_SOURCE_DIR}/bin)
    set(REPORT_PATH "${CMAKE_BINARY_DIR}/valgrind-${target}")
    add_custom_target(memcheck-${target}
        COMMAND ${MEMCHECK_PATH}/memcheck_runner.sh -o
            "${REPORT_PATH}/report"
            -- $<TARGET_FILE:${target}>
        COMMAND ${MEMCHECK_PATH}/generate_html_report.sh
            -i ${REPORT_PATH}
            -o ${REPORT_PATH}
        WORKING_DIRECTORY ${CMAKE_BINARY_DIR}
    )
endfunction()
\end{cmake}

我们稍微改进了第12章中的AddMemcheck()函数以处理多个目标。我们让REPORT\_PATH变量成为目标特定的。

为了生成Memcheck报告，请使用以下命令（请注意，当使用Debug构建类型进行配置时，生成报告更为有效）：

\begin{shell}
cmake --build <build-tree> -t memcheck-calc_test
cmake --build <build-tree> -t memcheck-calc_console_test
\end{shell}

好的，我们定义了Coverage和Memcheck模块（它们在Testing模块中使用），现在让我们看看实际的测试目标是如何配置的。

\mySubsubsection{15.5.3.}{应用测试场景}

为了实施测试，我们将遵循以下场景：

\begin{enumerate}
\item
编写单元测试。

\item
使用AddTests()定义和配置测试的可执行目标。

\item
为被测软件（Software Under Test, SUT）配置度量以启用覆盖率收集。

\item
确保在构建之间清除覆盖率数据以防止分段错误。
\end{enumerate}

让我们从要编写的单元测试开始。为了保持简洁，我们将提供最简单的（也许有点不完整）单元测试。首先，测试库：

\filename{ch15/01-full-project/test/calc/basic\_test.cpp}

\begin{cmake}
#include "calc/basic.h"
#include <gtest/gtest.h>

TEST(CalcTest, SumAddsTwoInts) {
    EXPECT_EQ(4, Calc::Add(2, 2));
}

TEST(CalcTest, SubtractsTwoInts) {
    EXPECT_EQ(6, Calc::Subtract(8, 2));
}
\end{cmake}

接着是针对控制台的测试——为此，我们将使用FXTUI库。再次，完全理解源代码并非必要；这些测试仅用于说明目的：

\filename{ch15/01-full-project/test/calc\_console/tui\_test.cpp}

\begin{cmake}
#include "tui.h"

#include <gmock/gmock.h>
#include <gtest/gtest.h>

#include <ftxui/screen/screen.hpp>

using namespace ::ftxui;

TEST(ConsoleCalcTest, RunWorksWithDefaultValues) {
    auto component = getTui();
    auto document = component->Render();
    auto screen = Screen::Create(Dimension::Fit(document));
    Render(screen, document);
    auto output = screen.ToString();
    ASSERT_THAT(output, testing::HasSubstr("Sum: 102"));
}
\end{cmake}

此测试将UI渲染到静态Screen对象，并检查字符串输出是否包含预期的总和。这不是一个很好的测试，但至少它很短。

现在，让我们使用两个嵌套的列表文件来配置我们的测试。首先，为库：

\filename{ch15/01-full-project/test/calc/CMakeLists.txt}

\begin{cmake}
add_executable(calc_test basic_test.cpp)
target_link_libraries(calc_test PRIVATE calc_static)
AddTests(calc_test)
\end{cmake}

然后，为可执行文件：

\filename{ch15/01-full-project/test/calc\_console/CMakeLists.txt}

\begin{cmake}
add_executable(calc_console_test tui_test.cpp)
target_link_libraries(calc_console_test
                      PRIVATE calc_console_static)
AddTests(calc_console_test)
\end{cmake}

这些配置使得CTest可以执行测试。我们还需要为业务逻辑目标准备覆盖率分析，并确保覆盖率数据在构建之间得到刷新。

让我们向calc库目标添加必要的指令：

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (续)}

\begin{cmake}
# ... calc_obj target definition

InstrumentForCoverage(calc_obj)

# ... calc_shared target definition
# ... calc_static target definition

CleanCoverage(calc_static)
\end{cmake}

为calc\_obj添加了InstrumentForCoverage，带有额外的-{}-coverage标志，但是CleanCoverage()是为calc\_static目标调用的。通常情况下，你会出于一致性考虑将其应用于calc\_obj，但我们在这里使用的是PRE\_BUILD关键字，而CMake不允许对对象库使用PRE\_BUILD、PRE\_LINK或POST\_BUILD钩子。

最后，我们还将为控制台目标添加仪器化和清理指令：

\filename{ch15/01-full-project/src/calc\_console/CMakeLists.txt (continued)}

\begin{cmake}
# ... calc_console_test target definition
# ... BuildInfo

InstrumentForCoverage(calc_console_static)
CleanCoverage(calc_console_static)
\end{cmake}

通过这些步骤，CTest现在已经设置好来运行我们的测试和收集覆盖率。下一步，我们将添加指令以启用静态分析，因为我们希望我们的项目在首次构建和后续所有构建中都能达到高质量。

\mySubsubsection{15.5.4.}{添加静态分析工具}

我们正在接近为目标配置质量保证的完成阶段。最后一步涉及到启用自动格式化和集成CppCheck：

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (续)}

\begin{cmake}
# ... calc_static target definition
# ... Coverage instrumentation and cleaning

Format(calc_static .)

AddCppCheck(calc_obj)
\end{cmake}

这里我们遇到了一个小问题：calc\_obj不能有PRE\_BUILD钩子，所以我们改为对calc\_static应用格式化。我们还确保calc\_console\_static目标进行了格式化和检查：

\filename{ch15/01-full-project/src/calc\_console/CMakeLists.cmake (续)}

\begin{cmake}
# ... calc_console_test target definition
# ... BuildInfo
# ... Coverage instrumentation and cleaning

Format(calc_console_static .)

AddCppCheck(calc_console_static)
\end{cmake}

我们还需要定义Format和CppCheck函数。从Format()开始，我们借用第12章《程序分析工具》中描述的代码：

\filename{ch15/01-full-project/cmake/Format.cmake}

\begin{cmake}
function(Format target directory)
    find_program(CLANG-FORMAT_PATH clang-format REQUIRED)
    set(EXPRESSION h hpp hh c cc cxx cpp)
    list(TRANSFORM EXPRESSION PREPEND "${directory}/*.")
    file(GLOB_RECURSE SOURCE_FILES FOLLOW_SYMLINKS
        LIST_DIRECTORIES false ${EXPRESSION}
    )
    add_custom_command(TARGET ${target} PRE_BUILD COMMAND
        ${CLANG-FORMAT_PATH} -i --style=file ${SOURCE_FILES}
    )
endfunction()
\end{cmake}

为了将CppCheck与我们的源码集成，我们使用：

\filename{ch15/01-full-project/cmake/CppCheck.cmake}

\begin{cmake}
function(AddCppCheck target)
    find_program(CPPCHECK_PATH cppcheck REQUIRED)
    set_target_properties(${target}
    PROPERTIES CXX_CPPCHECK
        "${CPPCHECK_PATH};--enable=warning;--error-exitcode=10"
    )
endfunction()
\end{cmake}

这是简单且方便的做法。您可能会发现这与Clang-Tidy模块（来自第12章《程序分析工具》）展示出CMake功能的一致性。

cppcheck的参数如下：

\begin{itemize}
\item
-{}-enable=warning: 启用警告消息。要启用更多检查，请参阅Cppcheck手册（见进一步阅读部分）。

\item
-{}-error-exitcode=1: 设置cppcheck检测到问题时返回的错误码。这可以是1到255之间的任何数字（0表示成功），尽管有些数字可能被系统保留。
\end{itemize}

随着src和test目录中的所有文件创建完毕，我们的解决方案现在可以构建并进行全面测试。我们可以继续进行安装和打包步骤。

































