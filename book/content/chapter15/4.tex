
所有的构建过程都遵循同样的程序。我们从顶层列表文件开始，并向下遍历项目的源代码树。图15.4展示了构建过程中涉及的项目文件，括号内的数字表示CMake脚本执行的顺序。

\myGraphic{0.8}{content/chapter15/images/4.png}{图 15.4: 构建阶段使用的文件}

顶层的CMakeLists.txt (1) 配置项目：

\filename{ch15/01-full-project/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(Calc VERSION 1.1.0 LANGUAGES CXX)
list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")

include(NoInSourceBuilds)

include(CTest)

add_subdirectory(src bin)
add_subdirectory(test)

include(Packaging)
\end{cmake}

我们首先指定基本的项目详情并设置CMake实用模块的路径（项目中的cmake目录）。接着我们使用一个自定义模块来防止原地构建。

随后，我们使用内置的CTest模块启用测试。这应该在项目的根级别完成，因为这条命令会在二进制树中创建CTestTestfile.cmake文件，相对于其在源码树的位置。如果放在其他地方，则ctest将无法找到它。

接下来，我们包含两个关键目录：

\begin{itemize}
\item
src, 包含项目源代码（在构建树中命名为bin）

\item
test, 包含所有测试工具
\end{itemize}

最后，我们包含Packaging模块，相关细节将在安装和打包部分讨论。

让我们来查看NoInSourceBuilds实用模块，以理解它的功能：

\filename{ch15/01-full-project/cmake/NoInSourceBuilds.cmake}

\begin{cmake}
if(PROJECT_SOURCE_DIR STREQUAL PROJECT_BINARY_DIR)
    message(FATAL_ERROR
        "\n"
        "In-source builds are not allowed.\n"
        "Instead, provide a path to build tree like so:\n"
        "cmake -B <destination>\n"
        "\n"
        "To remove files you accidentally created execute:\n"
        "rm -rf CMakeFiles CMakeCache.txt\n"
    )
endif()
\end{cmake}

这里没有什么意外，我们检查用户是否使用cmake命令提供了单独的目的地目录来存放生成的文件。它必须与项目的源码树路径不同。如果没有，我们会指导用户如何指定目的地，并如何清理仓库中的错误文件。

我们的顶层列表文件接着包含了src子目录，指示CMake处理其中的列表文件：

\filename{ch15/01-full-project/src/CMakeLists.txt}

\begin{cmake}
include(Coverage)
include(Format)
include(CppCheck)
include(Doxygen)

add_subdirectory(calc)
add_subdirectory(calc_console)
\end{cmake}

这个文件很简单——它包含了我们将使用的来自./cmake目录的所有模块，并指示CMake执行这些目录中的列表文件。

接下来，我们来看一下calc库的列表文件。这个文件有些复杂，我们将逐段解析并讨论。

\mySubsubsection{15.4.1.}{构建Calc库}

calc目录中的列表文件配置了这个库的各个方面，但现在我们只关注构建部分：

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (片段)}

\begin{cmake}
add_library(calc_obj OBJECT basic.cpp)
target_sources(calc_obj
               PUBLIC FILE_SET HEADERS
               BASE_DIRS include
               FILES include/calc/basic.h
)
set_target_properties(calc_obj PROPERTIES
    POSITION_INDEPENDENT_CODE 1
)

# ... instrumentation of calc_obj for coverage

add_library(calc_shared SHARED)
target_link_libraries(calc_shared calc_obj)

add_library(calc_static STATIC)
target_link_libraries(calc_static calc_obj)

# ... testing and program analysis modules
# ... documentation generation
# ... installation
\end{cmake}

We define three targets:

\begin{itemize}
\item
calc\_obj, 一个对象库，编译basic.cpp实现文件。它的basic.h头文件通过target\_sources()命令中的FILE\_SET关键字被包含进来。这隐式地配置了适当的包含目录，以便在构建和安装模式下正确导出。通过创建一个对象库，我们避免了为两个库版本重复编译，但重要的是要启用POSITION\_INDEPENDENT\_CODE，以便共享库可以依赖这个目标。

\item
calc\_shared, 一个依赖于calc\_obj的共享库。

\item
calc\_static, 一个也依赖于calc\_obj的静态库。
\end{itemize}

作为上下文，这里是basic库的C++头文件。这个头文件简单地声明了Calc命名空间中的两个函数，有助于避免名称冲突：

\filename{ch15/01-full-project/src/calc/include/calc/basic.h}

\begin{cpp}
#pragma once
namespace Calc {
    int Add(int a, int b);
    int Subtract(int a, int b);
} // namespace Calc
\end{cpp}

实现文件也很简单：

\filename{ch15/01-full-project/src/calc/basic.cpp}

\begin{cpp}
namespace Calc {
    int Add(int a, int b) {
        return a + b;
    }

    int Subtract(int a, int b) {
        return a - b;
    }
} // namespace Calc
\end{cpp}

这就完成了对src/calc目录中文件的解释。接下来是src/calc\_console目录以及使用这个库构建控制台计算器的可执行文件。

\mySubsubsection{15.4.2.}{构建Calc可执行文件}

calc\_console目录包含几个文件：一个列表文件、两个实现文件（业务逻辑和引导文件）以及一个头文件。列表文件如下：

\filename{ch15/01-full-project/src/calc\_console/CMakeLists.txt (fragment)}

\begin{cmake}
add_library(calc_console_static STATIC tui.cpp)
target_include_directories(calc_console_static PUBLIC include)
target_precompile_headers(calc_console_static PUBLIC <string>)

include(GetFTXUI)
target_link_libraries(calc_console_static PUBLIC calc_shared
                      ftxui::screen ftxui::dom ftxui::component)

include(BuildInfo)
BuildInfo(calc_console_static)

# ... instrumentation of calc_console_static for coverage
# ... testing and program analysis modules
# ... documentation generation

add_executable(calc_console bootstrap.cpp)
target_link_libraries(calc_console calc_console_static)

# ... installation
\end{cmake}

尽管列表文件看起来很复杂，但作为经验丰富的CMake用户，我们现在很容易解读其内容：

\begin{enumerate}
\item
定义calc\_console\_static目标，包含不含main()函数的业务代码，以允许与具有自己入口点的GTest链接。

\item
配置包含目录。我们可以通过FILE\_SET逐个添加头文件，但由于它们是内部的，我们简化了这一步骤。

\item
实现头文件预编译，这里以<string>头文件为例演示，虽然大型项目可能会包含更多的头文件。

\item
包含一个自定义的CMake模块来获取FTXUI依赖项。

\item
将业务代码与calc\_shared共享库和FTXUI组件链接起来。

\item
添加一个自定义模块来生成构建信息，并将其嵌入到工件中。

\item
概述了针对此目标的其他步骤：coverage配置、测试、程序分析和文档生成。

\item
创建并链接calc\_console引导可执行文件，建立入口点。

\item
概述安装过程。
\end{enumerate}

我们将在本章后续的部分中探讨测试、文档和安装过程。

我们包含了GetFTXUI实用模块而不是寻找系统中的配置模块，因为大多数用户不太可能已经安装了它。我们将只是获取并构建它：

\filename{ch15/01-full-project/cmake/GetFTXUI.cmake}

\begin{cmake}
include(FetchContent)
FetchContent_Declare(
    FTXTUI
    GIT_REPOSITORY https://github.com/ArthurSonzogni/FTXUI.git
    GIT_TAG v0.11
)
option(FTXUI_ENABLE_INSTALL "" OFF)
option(FTXUI_BUILD_EXAMPLES "" OFF)
option(FTXUI_BUILD_DOCS "" OFF)
FetchContent_MakeAvailable(FTXTUI)
\end{cmake}

我们正在使用推荐的FetchContent方法，详细描述见第9章，《CMake中的依赖管理》。唯一不寻常的添加是option()命令的调用，这让我们可以绕过FTXUI的冗长构建步骤，并阻止其安装步骤影响本项目的安装过程。更多详情，请参考进一步阅读部分。

calc\_console目录的列表文件还包括另一个与构建相关的自定义实用模块：BuildInfo。这个模块将捕获三块信息以在可执行文件中显示：

\begin{itemize}
\item
当前Git提交的SHA

\item
构建时间戳

\item
在顶层列表文件中指定的项目版本
\end{itemize}

正如我们在第7章，《使用CMake编译C++源代码》中学到的，CMake可以捕获构建时的值并通过模板文件传递给C++代码，例如通过一个结构体：

\filename{ch15/01-full-project/cmake/buildinfo.h.in}

\begin{cpp}
struct BuildInfo {
    static inline const std::string CommitSHA = "@COMMIT_SHA@";
    static inline const std::string Timestamp = "@TIMESTAMP@";
    static inline const std::string Version = "@PROJECT_VERSION@";
};
\end{cpp}

为了在配置阶段填充这个结构体，我们将使用以下代码：

\filename{ch15/01-full-project/cmake/BuildInfo.cmake}

\begin{cmake}
set(BUILDINFO_TEMPLATE_DIR ${CMAKE_CURRENT_LIST_DIR})
set(DESTINATION "${CMAKE_CURRENT_BINARY_DIR}/buildinfo")
string(TIMESTAMP TIMESTAMP)

find_program(GIT_PATH git REQUIRED)
execute_process(COMMAND ${GIT_PATH} log --pretty=format:'%h' -n 1
                OUTPUT_VARIABLE COMMIT_SHA)

configure_file(
    "${BUILDINFO_TEMPLATE_DIR}/buildinfo.h.in"
    "${DESTINATION}/buildinfo.h" @ONLY
)

function(BuildInfo target)
    target_include_directories(${target} PRIVATE ${DESTINATION})
endfunction()
\end{cmake}

在包含该模块后，我们设置了变量来捕获所需的信息，并使用configure\_file()来生成buildinfo.h。最后一步是调用BuildInfo函数，将生成文件的目录加入到目标的包含目录中。

产生的头文件可以根据需要与多个不同的消费者共享。在这种情况下，您可能想要在列表文件的顶部添加include\_guard(GLOBAL)来避免为每个目标运行git命令。

在深入了解控制台计算器的实现之前，我想强调不需要深入理解tui.cpp文件或FTXUI库的复杂性，因为这对于我们的目的来说不是必需的。相反，让我们专注于代码中的突出部分：

\filename{ch15/01-full-project/src/calc\_console/tui.cpp}

\begin{cpp}
#include "tui.h"
#include <ftxui/dom/elements.hpp>
#include "buildinfo.h"
#include "calc/basic.h"

using namespace ftxui;
using namespace std;

string a{"12"}, b{"90"};
auto input_a = Input(&a, "");
auto input_b = Input(&b, "");
auto component = Container::Vertical({input_a, input_b});

Component getTui() {
    return Renderer(component, [&] {
        auto sum = Calc::Add(stoi(a), stoi(b));
        return vbox({
            text("CalcConsole " + BuildInfo::Version),
            text("Built: " + BuildInfo::Timestamp),
            text("SHA: " + BuildInfo::CommitSHA),
            separator(),
            input_a->Render(),
            input_b->Render(),
            separator(),
            text("Sum: " + to_string(sum)),
        }) |
        border;
    });
}
\end{cpp}

这段代码提供了getTui()函数，该函数返回一个ftxui::Component对象，这是一个封装了交互式UI元素的对象，如标签、文本字段、分割线和边框。对于那些对这些元素的详细工作原理感兴趣的人，在进一步阅读部分有更多资料可供参考。

更重要的是，包含指令链接到了calc\_obj目标的头文件和BuildInfo模块。交互从lambda函数开始，调用Calc::Add，并使用text()函数显示结果。

在构建时收集的buildinfo.h中的值以类似的方式使用，并将在运行时显示给用户。

除了tui.cpp外，还有一个头文件：

\filename{ch15/01-full-project/src/calc\_console/include/tui.h}

\begin{cpp}
#include <ftxui/component/component.hpp>
ftxui::Component getTui();
\end{cpp}

这个头文件被calc\_console目标中的引导文件使用：

\filename{ch15/01-full-project/src/calc\_console/bootstrap.cpp}

\begin{cpp}
#include <ftxui/component/screen_interactive.hpp>

#include "tui.h"

int main(int argc, char** argv) {
    ftxui::ScreenInteractive::FitComponent().Loop(getTui());
}
\end{cpp}

这段简短的代码使用FTXUI初始化了一个交互式的控制台屏幕，显示getTui()返回的Component对象，并在一个循环中处理键盘输入。现在所有src目录中的文件都已经介绍过了，我们可以继续进行测试和程序分析。


























