
图15.6展示了如何配置项目以便进行安装和打包：

\myGraphic{1.0}{content/chapter15/images/6.png}{图15.6: 配置安装和打包的文件}

顶层列表文件包含了Packaging模块：

\filename{ch15/01-full-project/CMakeLists�txt (片段)}

\begin{cmake}
# ... configure project
# ... enable testing
# ... include src and test subdirectories

include(Packaging)
\end{cmake}

Packaging模块详细说明了项目的包配置，这部分将在使用CPack进行打包的部分进行探讨。现在，关注的是安装三个主要组件：

\begin{itemize}
\item
Calc库工件：静态和共享库、头文件以及目标导出文件

\item
Calc库的包定义配置文件

\item
Calc控制台可执行文件
\end{itemize}

一切已经计划好了，现在是时候安装库。

\mySubsubsection{15.6.1.}{安装库}

为了安装库，首先定义逻辑目标及其工件的目的地，利用GNUInstallDirs模块的默认值以避免手动指定路径。工件将分组到组件中，默认安装会安装所有文件，可以选择只安装运行时组件，并跳过开发工件：

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (续)}

\begin{cmake}
# ... calc library targets definition
# ... configuration, testing, program analysis

# Installation
include(GNUInstallDirs)
install(TARGETS calc_obj calc_shared calc_static
    EXPORT CalcLibrary
    ARCHIVE COMPONENT development
    LIBRARY COMPONENT runtime
    FILE_SET HEADERS COMPONENT runtime
)
\end{cmake}

对于UNIX系统，我们还配置了安装后注册共享库到ldconfig：

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (续)}

\begin{cmake}
if (UNIX)
    install(CODE "execute_process(COMMAND ldconfig)"
        COMPONENT runtime
    )
endif()
\end{cmake}

为了使其他CMake项目能够复用，将通过生成并安装一个目标导出文件和一个引用它的配置文件来打包库：

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (续)}

\begin{cmake}
install(EXPORT CalcLibrary
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/calc/cmake
    NAMESPACE Calc::
    COMPONENT runtime
)

install(FILES "CalcConfig.cmake"
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/calc/cmake
)
\end{cmake}

为了简化，CalcConfig.cmake文件保持最小化：

\filename{ch15/01-full-project/src/calc/CalcConfig.cmake}

\begin{cmake}
include("${CMAKE_CURRENT_LIST_DIR}/CalcLibrary.cmake")
\end{cmake}

这个文件位于src/calc目录下，它只包含了库目标。如果还有其他目录的目标定义，比如calc\_console，通常会把CalcConfig.cmake放在顶层目录或src目录下。

现在，库已经准备好通过cmake -{}-install命令在构建项目后进行安装。不过，还需要配置可执行文件的安装。

\mySubsubsection{15.6.2.}{可执行文件的安装}

当然，我们希望用户能够在他们的系统中使用可执行文件，因此需要使用CMake来安装。准备二进制可执行文件的安装很简单；为了实现这一点，只需要包含GNUInstallDirs并使用install()命令：

\filename{ch15/01-full-project/src/calc\_console/CMakeLists.txt (续)}

\begin{cmake}
# ... calc_console_static definition
# ... configuration, testing, program analysis
# ... calc_console bootstrap executable definition

# Installation
include(GNUInstallDirs)
install(TARGETS calc_console
    RUNTIME COMPONENT runtime
)
\end{cmake}

这样，可执行文件就设置好进行安装了。现在，继续进行打包。

\mySubsubsection{15.6.3.}{使用CPack打包}

可以尽情配置大量的支持的包类型；但对于这个项目，基本配置就够了：

\filename{ch15/01-full-project/cmake/Packaging.cmake}

\begin{cmake}
# CPack configuration
set(CPACK_PACKAGE_VENDOR "Rafal Swidzinski")
set(CPACK_PACKAGE_CONTACT "email@example.com")
set(CPACK_PACKAGE_DESCRIPTION "Simple Calculator")
include(CPack)
\end{cmake}

这样的最小化配置对于标准存档，如ZIP文件，工作得很好。为了在构建项目后测试安装和打包过程，请在构建树内使用以下命令：

\begin{shell}
# cpack -G TGZ -B packages
CPack: Create package using TGZ
CPack: Install projects
CPack: - Run preinstall target for: Calc
CPack: - Install project: Calc []
CPack: Create package
CPack: - package: .../packages/Calc-1.0.0-Linux.tar.gz generated.
\end{shell}

这就完成了安装和打包的过程；接下来的任务是提供文档。















