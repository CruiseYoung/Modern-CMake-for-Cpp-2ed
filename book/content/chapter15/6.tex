
Figure 15.6 shows where we’ll configure our project for installation and packaging:

\myGraphic{0.8}{content/chapter15/images/6.png}{Figure 15.6: File configuring installation and packaging}

The top-level listfile includes the Packaging utility module:

\filename{ch15/01-full-project/CMakeLists�txt (fragment)}

\begin{cmake}
# ... configure project
# ... enable testing
# ... include src and test subdirectories

include(Packaging)
\end{cmake}

The Packaging module details the package configuration for the project, which we will explore in the Packaging with CPack section. Our focus now is on installing three main components:

\begin{itemize}
\item
The Calc library artifacts: static and shared libraries, header files, and target export files

\item
The package definition config file for the Calc library

\item
The Calc console executable
\end{itemize}

Everything is planned, so it’s time to configure the installation of the library.

\mySubsubsection{15.6.1.}{Installation of the library}

To install the library, we start by defining logical targets and their artifact destinations, utilizing the GNUInstallDirs module’s default values to avoid manual path specification. Artifacts will be grouped into components. The default installation will install all files, but you may choose to only install the runtime component and skip the development artifacts:

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (continued)}

\begin{cmake}
# ... calc library targets definition
# ... configuration, testing, program analysis

# Installation
include(GNUInstallDirs)
install(TARGETS calc_obj calc_shared calc_static
    EXPORT CalcLibrary
    ARCHIVE COMPONENT development
    LIBRARY COMPONENT runtime
    FILE_SET HEADERS COMPONENT runtime
)
\end{cmake}

For UNIX systems, we also configure post-installation registration of the shared library with ldconfig:

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (continued)}

\begin{cmake}
if (UNIX)
    install(CODE "execute_process(COMMAND ldconfig)"
        COMPONENT runtime
    )
endif()
\end{cmake}

To enable reusability in other CMake projects, we’ll package the library by generating and installing a target export file and a config file that references it:

\filename{ch15/01-full-project/src/calc/CMakeLists.txt (continued)}

\begin{cmake}
install(EXPORT CalcLibrary
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/calc/cmake
    NAMESPACE Calc::
    COMPONENT runtime
)

install(FILES "CalcConfig.cmake"
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/calc/cmake
)
\end{cmake}

For simplicity, the CalcConfig.cmake file is kept minimal:

\filename{ch15/01-full-project/src/calc/CalcConfig.cmake}

\begin{cmake}
include("${CMAKE_CURRENT_LIST_DIR}/CalcLibrary.cmake")
\end{cmake}

This file is located in src/calc since it only includes the library targets. If there were target definitions from other directories, like calc\_console, you would typically place CalcConfig.cmake in the top-level or src directory.

Now, the library is prepared to be installed with the cmake -{}-install command after building the project. However, we still need to configure the installation of the executable.

\mySubsubsection{15.6.2.}{Installation of the executable}

We, of course, want our users to be able to enjoy the executable in their system, so we will install it with CMake. Preparing the installation of the binary executable is straightforward; to achieve it, we only need to include GNUInstallDirs and use a single install() command:

\filename{ch15/01-full-project/src/calc\_console/CMakeLists.txt (continued)}

\begin{cmake}
# ... calc_console_static definition
# ... configuration, testing, program analysis
# ... calc_console bootstrap executable definition

# Installation
include(GNUInstallDirs)
install(TARGETS calc_console
    RUNTIME COMPONENT runtime
)
\end{cmake}

With that, the executable is set to be installed. Now, let’s proceed to packaging.

\mySubsubsection{15.6.3.}{Packaging with CPack}

We could go wild and configure a vast multitude of supported package types; for this project, however, a basic configuration will be enough:

\filename{ch15/01-full-project/cmake/Packaging.cmake}

\begin{cmake}
# CPack configuration
set(CPACK_PACKAGE_VENDOR "Rafal Swidzinski")
set(CPACK_PACKAGE_CONTACT "email@example.com")
set(CPACK_PACKAGE_DESCRIPTION "Simple Calculator")
include(CPack)
\end{cmake}

Such a minimal setup works well for standard archives, such as ZIP files. To test the installation and packaging processes after building the project, use the following command within the build tree:

\begin{shell}
# cpack -G TGZ -B packages
CPack: Create package using TGZ
CPack: Install projects
CPack: - Run preinstall target for: Calc
CPack: - Install project: Calc []
CPack: Create package
CPack: - package: .../packages/Calc-1.0.0-Linux.tar.gz generated.
\end{shell}

This concludes the installation and packaging; the next order of business is documentation.















