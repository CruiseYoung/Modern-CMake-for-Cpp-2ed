
链接器的行为有时可能显得反复无常，无缘无故地抛出错误。这对于不熟悉这个工具复杂性的初级开发者来说，常常是一个特别棘手的挑战。他们通常会尽可能长时间地避免处理构建配置，但当需要做出更改时——也许是要整合他们开发的库——问题就会全面爆发。

考虑以下情况：一个相对直接的依赖链，其中主可执行文件依赖于一个“外部”库。而这个外部库又依赖于包含必要变量int b的“嵌套”库。突然之间，一个神秘的错误信息出现在程序员面前：

\begin{shell}
outer.cpp:(.text+0x1f): undefined reference to 'b'
\end{shell}

此类错误很常见。通常，表示链接器中忘记了一个库。然而，在这个场景中，库似乎已经正确地添加到了target\_link\_libraries()命令中：

\filename{ch08/06-unresolved/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(Order CXX)
add_library(outer outer.cpp)
add_library(nested nested.cpp)
add_executable(main main.cpp)
target_link_libraries(main nested outer)
\end{cmake}

问题是什么呢！？很少有错误能像这样调试和理解时让人如此愤怒。我们看到的是链接顺序不正确：

\filename{ch08/06-unresolved/main.cpp}

\begin{cmake}
#include <iostream>
extern int a;
int main() {
    std::cout << a << std::endl;
}
\end{cmake}

代码看起来很简单——将打印外部变量a，该变量可以在outer库中找到。提前使用extern关键字声明，以下是该库的源码：

\filename{ch08/06-unresolved/outer.cpp}

\begin{cmake}
extern int b;
int a = b;
\end{cmake}

这相当简单——outer依赖于嵌套库提供外部变量b，该变量赋值给变量a。来看看nested的源代码，以确认没有遗漏定义：

\filename{ch08/06-unresolved/nested.cpp}

\begin{cmake}
int b = 123;
\end{cmake}

确实，我们为b提供了定义，并且由于它没有使用static关键字标记为局部变量，因此会从nested目标正确导出。如我们之前所见，此目标在CMakeLists.txt中与主可执行文件链接：

\begin{cmake}
target_link_libraries(main nested outer)
\end{cmake}

那么，对’b’的未定义引用错误从何而来？解析未定义的符号是这样工作的——链接器从左到右处理二进制文件。

当链接器遍历二进制文件时，将执行以下操作：

\begin{enumerate}
\item
收集此二进制文件导出的所有未定义符号，并存储起来以备后用。

\item
尝试用此二进制文件中定义的符号解析未定义的符号（来自迄今为止处理的所有二进制文件）。

\item
对下一个二进制文件重复此过程。
\end{enumerate}

如果在整个操作完成后仍有符号未定义，链接将失败。这就是我们示例中的情况（CMake将可执行文件目标的对象文件放在库的前面）：

\begin{enumerate}
\item
链接器处理了main.o，发现对a变量的未定义引用，并将其收集起来以备将来解析。

\item
链接器处理了libnested.a，没有发现未定义的引用，也没有需要解析的。

\item
链接器处理了libouter.a，发现对b变量的未定义引用，并解析了对a变量的引用。
\end{enumerate}

我们正确地解析了对a变量的引用，但未解析对b变量的引用。要纠正这一点，我们需要反转链接顺序，使nested在outer之后：

\begin{cmake}
arget_link_libraries(main outer nested)
\end{cmake}

有时，会遇到循环引用，其中翻译单元相互定义符号，没有一种有效的顺序可以满足所有引用。解决这个问题的唯一方法是对某些目标进行两次处理：

\begin{cmake}
target_link_libraries(main nested outer nested)
\end{cmake}

这是一种常见做法，但使用起来略显不雅。如果有幸使用CMake 3.24或更高版本，可以使用\$<LINK\_GROUP>生成器表达式与RESCAN功能，该功能添加了链接器特定的标志，如-{}-start-group或-{}-end-group，以确保能找到所有的符号：

\begin{cmake}
target_link_libraries(main "$<LINK_GROUP:RESCAN,nested,outer>")
\end{cmake}

这种机制引入了额外的处理步骤，应当只在必要时使用。需要循环引用的情况非常罕见（并且是合理的）。遇到这个问题通常表明设计不佳，其在Linux、BSD、SunOS，以及使用GNU工具链的Windows上得到支持。

现在准备处理ODR问题。我们还可能遇到哪些其他问题？链接过程中神秘地缺失符号。来看看这是关于什么的。

\mySubsubsection{8.5.1.}{处理未引用的符号}

当创建库时，尤其是静态库，基本上是由多个对象文件捆绑在一起的归档文件。我们提到过，一些归档工具可能会创建符号索引以加速链接过程。这些索引提供了每个符号与定义它们的对象文件之间的映射。当解析一个符号时，包含该符号的对象文件会合并到最终的二进制文件中（一些链接器通过只包含文件的具体部分来进一步优化）。如果静态库中的对象文件没有引用符号，则可能会完全忽略该对象文件。因此，只有实际使用的静态库部分，才会出现在最终的二进制文件中。

然而，可能需要一些未引用的符号：

\begin{itemize}
\item
静态初始化：如果库有需要在main()之前初始化的全局对象（即构造函数需要执行），并且这些对象在其它地方没有直接引用；链接器可能会将其从最终二进制文件中排除。

\item
插件架构：如果正在开发一个插件系统（带有模块库），其中代码需要在运行时进行识别和加载，而不需要直接引用。

\item
静态库中的未使用代码：如果正在开发一个包含实用函数或代码的静态库，这些代码并非总是直接引用，但仍然希望它们出现在最终二进制文件中。

\item
模板实例化：对于重度依赖模板的库；如果未明确提及，一些模板实例化可能会在链接过程中忽略。

\item
链接问题：特别是对于复杂的构建系统或详尽的代码库，链接可能会产生不可预测的结果，其中某些符号或代码部分似乎缺失。
\end{itemize}

这时，强制在链接过程中包含所有对象文件很有用，可通过一种称为“全归档链接”的模式来实现。

特定的编译器链接标志如下：

\begin{itemize}
\item
GCC使用 -{}-whole-archive

\item
Clang使用 -{}-force-load

\item
MSVC使用 /WHOLEARCHIVE
\end{itemize}

为了实现这一点，可以使用target\_link\_options()命令：

\begin{cmake}
target_link_options(tgt INTERFACE
    -Wl,--whole-archive $<TARGET_FILE:lib1> -Wl,--no-whole-archive
)
\end{cmake}

然而，这个命令是特定于链接器的，因此结合生成器表达式，来检测不同的编译器，并提供必要的标志。幸运的是，CMake 3.24引入了一个新的生成器表达式用于此目的：

\begin{cmake}
target_link_libraries(tgt INTERFACE
    "$<LINK_LIBRARY:WHOLE_ARCHIVE,lib1>"
)
\end{cmake}

使用这种方法可以确保tgt目标包含lib1库的所有对象文件。

尽管如此，还需要考虑一些潜在的问题：

\begin{itemize}
\item
增加二进制文件大小：这个标志可能会大幅增加最终二进制文件大小，其包含了指定库的所有对象文件，无论是否使用。

\item
符号冲突的可能性：引入所有符号可能会导致与其他符号冲突，从而引发链接错误。

\item
维护负担：过度依赖此类标志会掩盖代码设计或结构中的潜在问题。
\end{itemize}

了解了如何解决常见的链接挑战后，我们现在可以继续准备项目的测试了。




