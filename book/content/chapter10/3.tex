
本书主要讨论CMake 3.26，但CMake经常更新，版本3.28就在本章完成前就发布了。如果使用的是这个版本或更新的版本，可以通过将cmake\_minimum\_required()命令设置为VERSION 3.28.0来访使用最新功能。

另一方面，如果坚持使用旧版本，或者想要迎合可能尚未升级的更广泛的受众，需要启用实验性支持才能在CMake中使用C++20模块。

让我们探讨如何做到这一点。

\mySubsubsection{10.3.1.}{CMake 3.26和3.27中启用实验性支持}

实验性支持代表一种协议：作为开发者，承认这个功能尚未准备好投入生产，应仅用于测试目的。要签署这样的协议，需要在项目的列表文件中，将CMAKE\_EXPERIMENTAL\_CXX\_MODULE\_CMAKE\_API变量设置为CMake版本的一个特定值。

\begin{myNotic}{Note}
CMake的官方Kitware仓库托管了一个问题跟踪器，可以在其中搜索标签area:cxxmodules。直到3.28版本发布，只报告了一个问题（在3.25.0中），这是潜在稳定功能的一个很好的指标。如果决定启用实验，请构建项目，以确认它适用于用户。
\end{myNotic}

以下是在CMake的仓库和文档中可以找到的标志：

\begin{itemize}
\item
3c375311-a3c9-4396-a187-3227ef642046 对应 3.25 (无正式文件)

\item
2182bf5c-ef0d-489a-91da-49dbc3090d2a 对应 3.26

\item
aa1f7df0-828a-4fcd-9afc-2dc80491aca7 对应 3.27
\end{itemize}

如果不能访问CMake 3.25，那就头痛了，模块在该版本☞前不可用。此外，如果CMake版本早于3.27，还需要设置一个变量来为模块启用动态依赖：

\begin{cmake}
set(CMAKE_EXPERIMENTAL_CXX_MODULE_DYNDEP 1)
\end{cmake}

以下是如何为当前版本自动选择正确的API密钥，以及明确不支持版本构建（这个例子中，将只支持CMake 3.26及以上的版本）。

\filename{ch10/01-cxx-modules/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26.0)
project(CXXModules CXX)

# turn on the experimental API
if(CMAKE_VERSION VERSION_GREATER_EQUAL 3.28.0)
    # Assume that C++ sources do import modules
    cmake_policy(SET CMP0155 NEW)
elseif(CMAKE_VERSION VERSION_GREATER_EQUAL 3.27.0)
    set(CMAKE_EXPERIMENTAL_CXX_MODULE_CMAKE_API
        "aa1f7df0-828a-4fcd-9afc-2dc80491aca7")
elseif(CMAKE_VERSION VERSION_GREATER_EQUAL 3.26.0)
    set(CMAKE_EXPERIMENTAL_CXX_MODULE_CMAKE_API
        "2182bf5c-ef0d-489a-91da-49dbc3090d2a")
    set(CMAKE_EXPERIMENTAL_CXX_MODULE_DYNDEP 1)
else()
    message(FATAL_ERROR "Version lower than 3.26 not supported")
endif()
\end{cmake}

逐条解释：

\begin{enumerate}
\item
首先，检查版本是否为3.28或更新。允许使用cmake\_policy()启用CMP0155策略。如果想支持3.28以下的版本，这是必需的。

\item
如果不是这种情况，将检查版本是否高于3.27。如果是，将设置适当的API密钥。

\item
如果不低于3.27，将检查它是否高于3.26。如果是这样，设置适当的API密钥，并启用实验性C++20模块动态依赖标志。

\item
如果版本低于3.26，项目不支持，并将输出致命错误消息通知用户。
\end{enumerate}

这使我们能够支持从3.26开始的CMake版本范围。如果有幸在每个将构建项目的环境中运行CMake 3.28，则上述if()块是不必要的。那么什么是必要的呢？

\mySubsubsection{10.3.2.}{启用对CMake 3.28及以上的支持}

要从3.28开始使用C++20模块，必须明确声明这个版本为最低版本：

\begin{cmake}
cmake_minimum_required(VERSION 3.28.0)
project(CXXModules CXX)
\end{cmake}

如果将最低要求版本设置为3.28或以上，将默认启用CMP0155策略。继续阅读以了解在定义模块之前需要配置的东西。如果需要3.27或更低的版本，即使项目是使用CMake 3.28或更新的版本构建的，构建也可能会失败。

接下来要考虑的是编译器要求。

\mySubsubsection{10.3.3.}{设置编译器要求}

无论是使用CMake 3.26、3.27、3.28还是更新的版本来构建，为了使用C++模块创建解决方案，都需要设置两个全局变量。第一个是禁用不受支持的C++扩展，第二个是确保编译器支持所需的标准。

\filename{ch10/01-cxx-modules/CMakeLists.txt (续)}

\begin{cmake}
# Libc++ has no support compiler extensions for modules.
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD 20)
\end{cmake}

由于支持模块的编译器数量非常有限，设置标准可能看起来有些多余。然而，这对于保护项目未来不受影响是一种良好的习惯。

总体配置相当简单，到此结束。我们现在可以在CMake中定义一个模块。

\mySubsubsection{10.3.4.}{声明一个C++模块}

CMake模块定义利用了target\_sources()命令和FILE\_SET关键字：

\begin{cmake}
target_sources(math
    PUBLIC FILE_SET CXX_MODULES TYPE CXX_MODULES FILES math.cppm
)
\end{cmake}

这里，引入了一种新的文件集类型：CXX\_MODULES。这种类型默认情况下只在CMake 3.28及以后的版本中支持。对于3.26，需要启用实验性API。如果没有适当的支持，将会出现如下错误消息：

\begin{shell}
CMake Error at CMakeLists.txt:25 (target_sources):
target_sources File set TYPE may only be "HEADERS"
\end{shell}

如果在构建输出中看到这个，请检查代码是否正确。如果使用的版本的API密钥值不正确，也会出现这个消息。

如前所述，在同一个二进制文件中使用模块定义模块是有好处的。

但在创建库时，这些优势更为明显。这样的库可以在其他项目中使用，或者在同一项目中让其他库使用，从而进一步增强模块化。

要声明模块并将其与主程序链接，使用以下CMake配置：

\filename{ch10/01-cxx-modules/CMakeLists.txt (continued)}

\begin{cmake}
add_library(math)
target_sources(math
    PUBLIC FILE_SET CXX_MODULES FILES math.cppm
)
target_compile_features(math PUBLIC cxx_std_20)
set_target_properties(math PROPERTIES CXX_EXTENSIONS OFF)

add_executable(main main.cpp)
target_link_libraries(main PRIVATE math)
\end{cmake}

为了确保这个库可以在其他项目中使用，必须使用target\_compile\_features()命令并明确要求cxx\_std\_20。此外，还需要在目标级别重复设置CXX\_EXTENSIONS OFF。如果没有这个，CMake将生成错误并停止构建。这似乎有些多余，可能会在CMake的未来版本中得到解决。

项目设置完成后，终于到了构建它的时候了。
