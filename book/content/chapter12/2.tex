专业开发者通常会遵循规则。据说高级开发者知道何时打破规则，因为他们能够证明其必要性。另一方面，非常资深开发者通常避免打破规则，以节省时间解释他们的选择。关键是要关注真正影响产品的问题，而不是纠结于细节。

在编码风格和格式方面，开发者面临许多选择：我们应该使用制表符还是空格进行缩进？如果是空格，多少个？列或文件的字符限制应该是多少？这些选择通常不会改变程序的行为，但可能会引发无价值的长时间讨论。

尽管存在通用做法，但辩论往往围绕个人偏好和轶事证据展开。例如，选择每列80个字符而不是120个是任意的。重要的是保持一致的样式，因为不一致可能会妨碍代码的可读性。为确保一致性，建议使用像clang-format这样的格式化工具。这个工具可以通知我们代码是否格式不正确，甚至可以自动更正。以下是一个格式化代码的示例命令：

\begin{shell}
clang-format -i --style=LLVM filename1.cpp filename2.cpp
\end{shell}


-i 选项指示clang-format直接编辑文件，而 -{}-style 指定要使用的格式化风格，如LLVM、Google、Chromium、Mozilla、WebKit，或提供在文件中的自定义风格（更多细节请参见“进一步阅读”部分）。

当然，我们不想每次更改后都手动执行此命令；CMake应该作为构建过程的一部分来处理这个问题。我们已经知道如何在系统上定位clang-format（我们事先需要手动安装它）。我们尚未涵盖的是如何将此外部工具应用于所有源文件。为此，我们将创建一个方便的函数，可以从中cmake目录包含它：

\filename{ch12/01-formatting/cmake/Format.cmake}

\begin{cmake}
function(Format target directory)
    find_program(CLANG-FORMAT_PATH clang-format REQUIRED)
    set(EXPRESSION h hpp hh c cc cxx cpp)
    list(TRANSFORM EXPRESSION PREPEND "${directory}/*.")
    file(GLOB_RECURSE SOURCE_FILES FOLLOW_SYMLINKS
        LIST_DIRECTORIES false ${EXPRESSION}
    )
    add_custom_command(TARGET ${target} PRE_BUILD COMMAND
        ${CLANG-FORMAT_PATH} -i --style=file ${SOURCE_FILES}
    )
endfunction()
\end{cmake}

Format函数接受两个参数：target和directory。它将在构建目标之前格式化目录中的所有源文件。

从技术上讲，目录中的所有文件不必属于目标，且目标源代码可能会分布在多个目录中。然而，跟踪与目标相关的所有源文件和头文件是复杂的，尤其是当我们需要排除外部库的头文件时。在这种情况下，关注目录比关注逻辑目标更容易。我们可以为每个需要格式化的目录调用该函数。

此函数具有以下步骤：

\begin{enumerate}
\item
查找已安装的clang-format二进制文件。如果找不到二进制文件，REQUIRED关键字将使配置停止并报错。

\item
创建要格式化的文件扩展名列表（用作globbing表达式）。

\item
在每个表达式前加上目录路径。

\item
使用之前创建的列表递归搜索源文件和头文件，将找到的文件路径放入SOURCE\_FILES变量（但跳过找到的任何目录路径）。

\item
将格式化命令附加到目标的PRE\_BUILD步骤。
\end{enumerate}

这种方法适用于小型到中型代码库。对于大型代码库，我们可能需要将绝对文件路径转换为相对路径，并使用目录作为工作目录运行格式化命令。这可能是由于shell命令中的字符限制，通常在约13000个字符处达到上限。

让我们探讨如何实际使用这个函数。以下是我们项目的结构：

\begin{shell}
- CMakeLists.txt
- .clang-format
- cmake
  |- Format.cmake
- src
  |- CMakeLists.txt
  |- header.h
  |- main.cpp
\end{shell}

首先，我们设置项目并将cmake目录添加到模块路径中，以便稍后包含：

\filename{ch12/01-formatting/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(Formatting CXX)
enable_testing()
list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
add_subdirectory(src bin)
\end{cmake}

接下来，我们填充src目录的列表文件：

\filename{ch12/01-formatting/src/CMakeLists.txt}

\begin{cmake}
add_executable(main main.cpp)
include(Format)
Format(main .)
\end{cmake}

这很简单。我们创建一个名为main的可执行目标，包含Format.cmake模块，并调用当前目录（src）中main目标的Format()函数。

现在，我们需要一些未格式化的源文件。头文件包含一个简单的未使用函数：

\filename{ch12/01-formatting/src/header.h}

\begin{cpp}
int unused() { return 2 + 2; }
\end{cpp}

我们还将包含一个带有过多错误空白的源文件：

\filename{ch12/01-formatting/src/main.cpp}

\begin{cpp}
#include <iostream>
                                using namespace std;
                    int main() {
        cout << "Hello, world!" << endl;
                                            }
\end{cpp}

快完成了。我们只需要格式化器的配置文件，通过 -{}-style=file 命令行参数启用：


\filename{ch12/01-formatting/.clang-format}

\begin{shell}
BasedOnStyle: Google
ColumnLimit: 140
UseTab: Never
AllowShortLoopsOnASingleLine: false
AllowShortFunctionsOnASingleLine: false
AllowShortIfStatementsOnASingleLine: false
\end{shell}

ClangFormat 将扫描父目录以查找 .clang-format 文件，该文件指定了确切的格式化规则。这让我们可以自定义每一个细节。在我的情况下，我从 Google 的编码风格开始，并做了一些调整：设置 140 个字符的列限制，不使用制表符，并且不允许短循环、函数或单行 if 语句。

构建项目后（格式化在编译前自动进行），我们的文件看起来像这样：

\filename{ch12/01-formatting/src/header.h (formatted)}

\begin{cpp}
int unused() {
    return 2 + 2;
}
\end{cpp}

即使头文件没有被目标使用，它也被格式化了。短函数不能放在单行上，正如预期的那样，添加了新行。现在 main.cpp 文件看起来也很整洁。不需要的空白已经消失，缩进也标准化了：

\filename{ch12/01-formatting/src/main.cpp (formatted)}

\begin{cpp}
#include <iostream>
using namespace std;
int main() {
    cout << "Hello, world!" << endl;
}
\end{cpp}

自动化格式化在代码审查期间节省了时间。如果你曾经因为空白问题而不得不修改提交，你会知道这带来了多大的安慰。一致的格式化让你的代码毫不费力地保持清洁。

\begin{myNotic}{Note}
将格式化应用于整个代码库很可能会对仓库中的大多数文件造成一次性的大更改。如果你（或你的团队成员）正在进行一些工作，这可能会引起很多合并冲突。最好的做法是在所有挂起更改完成后协调这样的努力。如果这不可能，考虑逐步采用，或许可以按目录进行。你的团队成员会感激的。
\end{myNotic}

尽管格式化器在使代码视觉上一致方面表现出色，但它不是一个全面的程序分析工具。对于更高级的需求，需要设计用于静态分析的其他实用程序。










