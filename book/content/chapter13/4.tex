Doxygen Awesome 提供了一些额外的功能，这些功能可以通过在文档头部的 HTML <head> 标签内包含几个 JavaScript 代码片段来启用。这些功能非常有用，允许用户在浅色和深色模式之间切换、为代码片段添加复制按钮、提供段落标题的永久链接以及创建交互式的目录。

然而，实现这些功能需要将额外的代码复制到输出目录，并将其包含在生成的 HTML 文件中。

以下是需要在 </head> 标签前包含的 JavaScript 代码：

\filename{ch13/cmake/extra\_headers}

\begin{minted}[frame=single]{html}
<script type="text/javascript" src="$relpath^doxygen-awesome-darkmodetoggle.js"></script>
<script type="text/javascript" src="$relpath^doxygen-awesome-fragmentcopy-button.js"></script>
<script type="text/javascript" src="$relpath^doxygen-awesome-paragraphlink.js"></script>
<script type="text/javascript" src="$relpath^doxygen-awesome-interactivetoc.js"></script>

<script type="text/javascript">
    DoxygenAwesomeDarkModeToggle.init()
    DoxygenAwesomeFragmentCopyButton.init()
    DoxygenAwesomeParagraphLink.init()
    DoxygenAwesomeInteractiveToc.init()
</script>
\end{minted}

这段代码首先会包含几个 JavaScript 文件，然后初始化不同的扩展。不幸的是，这段代码不能简单地添加到某个变量中。相反，需要用一个自定义文件覆盖默认的头部文件。这种覆盖可以通过向 Doxygen 的 HTML\_HEADER 配置变量提供该文件的路径来完成。

为了创建一个自定义头部而无需硬编码全部内容，可以使用 Doxygen 的命令行工具来生成一个默认的头部文件，并在生成文档之前编辑它：

\begin{shell}
doxygen -w html header.html footer.html style.css
\end{shell}

尽管我们不会使用或更改 footer.html 或 style.css，但它们是必需的参数，所以无论如何都需要创建它们。

最后，需要自动在 </head> 标签前插入 ch13/cmake/extra\_headers 文件的内容以包含所需的 JavaScript。这可以通过 Unix 命令行工具 sed 来完成，其会就地编辑 header.html 文件：

\begin{shell}
sed -i '/<\/head>/r ch13/cmake/extra_headers' header.html
\end{shell}

现在需要用 CMake 语言来编写这些步骤。以下是实现这一目标的宏：

\filename{ch13/02-doxygen-nice/cmake/Doxygen.cmake (片段)}

\begin{cmake}
macro(UseDoxygenAwesomeExtensions)
    set(DOXYGEN_HTML_EXTRA_FILES
        ${doxygen-awesome-css_SOURCE_DIR}/doxygen-awesome-darkmode-toggle.js
        ${doxygen-awesome-css_SOURCE_DIR}/doxygen-awesome-fragment-copybutton.js
        ${doxygen-awesome-css_SOURCE_DIR}/doxygen-awesome-paragraph-link.js
        ${doxygen-awesome-css_SOURCE_DIR}/doxygen-awesome-interactive-toc.js
    )

    execute_process(
        COMMAND doxygen -w html header.html footer.html style.css
        WORKING_DIRECTORY ${PROJECT_BINARY_DIR}
    )
    execute_process(
        COMMAND sed -i
        "/<\\/head>/r ${PROJECT_SOURCE_DIR}/cmake/extra_headers"
        header.html
        WORKING_DIRECTORY ${PROJECT_BINARY_DIR}
    )
    set(DOXYGEN_HTML_HEADER ${PROJECT_BINARY_DIR}/header.html)
endmacro()
\end{cmake}

这段代码看起来很复杂，但仔细看过后，会发现它其实相当简单。

下面列出它的功能：

\begin{enumerate}
\item
将四个 JavaScript 文件复制到输出目录

\item
执行 doxygen 命令来生成默认的 HTML 文件

\item
执行 sed 命令将所需的 JavaScript 注入头部

\item
用自定义版本覆盖默认的头部
\end{enumerate}

为了完成集成，可以在启用基本样式表之后调用这个宏：

\filename{ch13/02-doxygen-nice/cmake/Doxygen.cmake (fragment)}

\begin{cmake}
function(Doxygen input output)
# …
    UseDoxygenAwesomeCss()
    UseDoxygenAwesomeExtensions()
# …
endfunction()
\end{cmake}

本例的完整代码以及实际示例可以在本书的在线仓库中找到，我建议你在实际环境中阅读和探索这些示例。

\begin{myNotic}{其他文档生成工具}
有许多其他工具没有在这本书中涵盖，我们主要关注由 CMake 支持的项目。不过，其中一些可能更适合各位的具体情况。如果愿意尝试新事物，可以访问两个我觉得有趣的项目的网站：

\begin{itemize}
\item
Adobe 的 Hyde (\url{https://github.com/adobe/hyde})：针对 Clang 编译器设计，Hyde 生成 Markdown 文件，这些文件可以像 Jekyll（https://jekyllrb.com/）这样的静态页面生成器消费，Jekyll 是 GitHub 支持的工具

\item
Standardese (\url{https://github.com/standardese/standardese})：使用 libclang 编译代码，并提供 HTML、Markdown、LaTeX 和手册页格式的输出，有望成为下一个 Doxygen。
\end{itemize}
\end{myNotic}










































