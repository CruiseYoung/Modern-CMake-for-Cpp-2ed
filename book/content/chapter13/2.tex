Doxygen是最成熟和流行的从C++源代码生成文档的工具。当我说“成熟”时，我是认真的：第一个版本是由Dimitri van Heesch在1997年10月发布的。从那时起，它已经广为流传，并且几乎有250名贡献者活跃地支持 (\url{https://github.com/doxygen/doxygen})。

可能会担心将Doxygen整合到，没有使用文档生成的较大项目中。确实，为每个函数添加注释可能看起来令人生畏。然而，我鼓励从小处着手。专注于记录最近在最新提交中处理过的元素。记住，即使是部分完整的文档，也比完全没有文档要好，并且它逐渐帮助建立对项目的更全面的理解。

Doxygen可以生成以下格式的文档：

\begin{itemize}
\item
超文本标记语言 (HTML)

\item
富文本格式 (RTF)

\item
可移植文档格式t (PDF)

\item
Lamport TeX (LaTeX)

\item
PostScript (PS)

\item
Unix 手册 (man 页面)

\item
微软HTML帮助手册 (.CHM)
\end{itemize}

如果使用Doxygen指定的格式，在代码中添加提供信息注释，其将解析这些注释以丰富输出文件。此外，将分析代码结构以生成有用的图表和图形。后者是可选的，其需要外部Graphviz工具 (\url{https://graphviz.org/})。

开发者首先应该考虑以下问题：项目的用户只会接收文档，还是自己生成文档（可能是从源代码构建时）？第一个选项意味着文档随二进制文件分发，在线提供，或者（不太优雅地）与源代码一起检入到仓库中。

这很重要，如果希望用户在构建过程中生成文档，将需要在系统中存在依赖项。这并不是一个严重的问题，因为Doxygen和Graphviz可以通过大多数包管理器获得，并且只需要一个简单的命令，例如在Debian上：

\begin{shell}
apt-get install doxygen graphviz
\end{shell}

Windows的二进制文件也可以在项目网站上找到（请参阅“扩展阅读”部分）。

然而，一些用户可能不太愿意安装这个工具。我们必须决定是为用户生成文档，还是让他们在需要时添加依赖项。项目也可以像第9章描述的那样，为用户自动添加。注意，Doxygen是用CMake构建的。

当Doxygen和Graphviz安装到系统中后，可以将生成过程添加到我们的项目中。与一些在线资源建议的相反，这并不像看起来那么困难或复杂。不需要创建外部配置文件，提供Doxygen可执行文件的路径，或添加自定义目标。自从CMake 3.9以来，可以使用FindDoxygen查找模块中的doxygen\_add\_docs()函数，其会设置文档目标。

\begin{shell}
doxygen_add_docs(targetName [sourceFilesOrDirs...]
  [ALL] [WORKING_DIRECTORY dir] [COMMENT comment])
\end{shell}

第一个参数指定目标名称，需要使用-t参数显式构建它，通过cmake生成构建树：

\begin{shell}
# cmake --build <build-tree> -t targetName
\end{shell}

或者，可以通过添加ALL参数来确保始终构建文档。WORKING\_DIRECTORY选项很直接，指定命令的运行目录。COMMENT选项设置的值将在文档生成开始之前显示，提供有用的信息或指令。

我们将遵循前几章的做法，并创建一个带有辅助函数的实用模块（以便可以在其他项目中重用：

\filename{ch13/01-doxygen/cmake/Doxygen.cmake}

\begin{cmake}
function(Doxygen input output)
    find_package(Doxygen)
    if (NOT DOXYGEN_FOUND)
        add_custom_target(doxygen COMMAND false
            COMMENT "Doxygen not found")
        return()
    endif()
    set(DOXYGEN_GENERATE_HTML YES)
    set(DOXYGEN_HTML_OUTPUT
        ${PROJECT_BINARY_DIR}/${output})
    doxygen_add_docs(doxygen
        ${PROJECT_SOURCE_DIR}/${input}
        COMMENT "Generate HTML documentation"
    )
endfunction()
\end{cmake}

该函数接受两个参数——输入和输出目录——并创建一个自定义的doxygen目标：

\begin{enumerate}
\item
首先，使用CMake内置的Doxygen查找模块来确定，系统中是否可用Doxygen。

\item
如果不可用，创建一个虚拟的doxygen目标，通知用户并运行false命令（在类Unix系统中返回1，导致构建失败）。我们在此处用return()终止函数。

\item
如果Doxygen可用，将其配置为在提供的输出目录中生成HTML输出。Doxygen非常可配置（更多信息请参阅官方文档）。要设置任何选项，只需按照示例调用set()，并在其名称前加上DOXYGEN\_。

\item
设置实际的doxygen目标。所有DOXYGEN\_变量都将被转发到Doxygen的配置文件中，并将从源树中提供的输入目录生成文档。
\end{enumerate}

如果文档由用户生成，那么第二步可能应该涉及安装Doxygen。

要使用这个函数，可以将其纳入我们项目的主列表文件中：

\filename{ch13/01-doxygen/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(Doxygen CXX)
enable_testing()
list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")
add_subdirectory(src bin)
include(Doxygen)
Doxygen(src docs)
\end{cmake}

一点也不难！构建doxygen目标将生成如下所示的HTML文档：

\myGraphic{0.9}{content/chapter13/images/1.png}{图13.1：使用Doxygen生成的类参考}

为了在成员函数文档中添加重要细节，可以在头文件中的C++方法声明前加上适当的注释：

\filename{ch13/01-doxygen/src/calc.h (片段)}

\begin{cpp}
/**
    Multiply... Who would have thought?
    @param a the first factor
    @param b the second factor
    @result The product
*/
int Multiply(int a, int b);
\end{cpp}

这种格式被称为Javadoc。重要的是，注释块要以双星号开始：/**。更多关于Doxygen的docblocks的描述可以在“扩展阅读”部分的链接中找到。带有此类注释的Multiply函数将呈现如下所示的图形：

\myGraphic{0.5}{content/chapter13/images/2.png}{图13.2：参数和结果的注释}

如果安装了Graphviz，Doxygen将检测到它并生成依赖关系图：

\myGraphic{0.9}{content/chapter13/images/3.png}{图13.3：Doxygen生成的继承和协作图}

通过直接从源代码生成文档，建立了一个过程，使得在开发周期中与代码更改同步快速更新成为可能。此外，代码审查期间很可能会注意到忽略的注释更新。

许多开发者表示担忧，Doxygen提供的设计看起来过时，这让他们犹豫是否向客户展示生成的文档。然而，这个问题有一个简单的解决方案。



















































