
CMake中的变量是一个出奇复杂的主题。不仅有三种变量类别——普通变量、缓存变量和环境变量，而且它们还存在于不同的变量作用域中，每个作用域如何影响其他作用域都有特定的规则。通常情况下，对这些规则的理解不足会成为错误和头疼的根源。我建议你仔细研究这一部分，并确保在继续之前理解所有概念。

让我们从CMake中变量的几个关键事实开始：

\begin{itemize}
\item
变量名是区分大小写的，并且几乎可以包含任何字符。

\item
所有变量在内部都存储为字符串，即使某些命令可以将它们解释为其他数据类型的值（甚至是列表！）。
\end{itemize}

基本的变量操作命令是set()和unset()，但还有其他可以改变变量值的命令，如string()和list()。

要声明一个普通变量，我们只需调用set()，提供变量名和值：

\filename{ch02/02-variables/set.cmake}

\begin{cmake}
set(MyString1 "Text1")
set([[My String2]] "Text2")
set("My String 3" "Text3")
message(${MyString1})
message(${My\ String2})
message(${My\ String\ 3})
\end{cmake}

正如你所看到的，使用方括号和引号参数允许在变量名中包含空格。然而，在稍后引用时，我们必须用反斜杠（$ \verb|\|$）来转义空格。因此，建议在变量名中只使用字母数字字符、破折号（-）和下划线（\_）。

还要避免使用以下开头的保留名称（大写、小写或混合大小写）：CMAKE\_、\_CMAKE\_，或下划线（\_），后跟任何CMake命令的名称。

要取消设置变量，我们可以使用以下方式：unset(MyString1)。

\begin{myNotic}{Note}
set()命令接受普通文本变量名作为其第一个参数，但message()命令使用包裹在\$\{\}语法中的变量引用。
\end{myNotic}

如果我们用\$\{\}语法包裹的变量提供给set()命令，会发生什么？

要回答这个问题，我们需要更好地理解变量引用。

\mySubsubsection{2.3.1}{变量引用}

我之前在“命令参数”部分简要提到了引用，因为它们用于引用引号和非引号参数。我们了解到，要创建对已定义变量的引用，需要使用\$\{\}语法，如下所示：message(\$\{MyString1\})

在评估时，CMake将按照从内到外的顺序遍历变量作用域，并用一个值替换\$\{MyString1\}，如果没有找到变量，则用空字符串替换（CMake不会产生任何错误消息）。这个过程也称为变量求值、扩展或插值。

插值是从内到外进行的，从最内层的花括号对开始，向外移动。例如，如果遇到\$\{MyOuter\$\{MyInner\}\}引用：

\begin{enumerate}
\item
CMake会首先尝试评估MyInner，而不是搜索名为MyOuter\$\{MyInner\}的变量。

\item
如果MyInner变量成功扩展，CMake将使用新形成的引用重复扩展过程，直到无法进一步扩展为止。
\end{enumerate}

为避免收到意外结果，建议不要在变量值中存储变量扩展令牌。

CMake将执行变量扩展到最大限度，并且在完成后，它将把得到的值作为参数传递给命令。这就是为什么当我们调用set(\$\{MyInner\} “Hi”);时，实际上不会更改MyInner变量，而是会更改以MyInner存储的值为名称的变量。通常，这不是我们想要的结果。

变量引用在变量类别方面的工作方式有些特殊，但一般来说，以下适用：

\begin{itemize}
\item
\$\{\}语法用于引用普通变量或缓存变量。

\item
\$ENV\{\}语法用于引用环境变量。

\item
\$CACHE\{\}语法用于引用缓存变量。
\end{itemize}

没错，使用\$\{\}，你可能会从一个类别或另一个类别获取值：如果在当前作用域中设置了普通变量，则使用普通变量；如果没有设置，或者取消了设置，CMake将使用同名的缓存变量。如果没有这样的变量，引用将评估为空字符串。

CMake预定义了许多内置普通变量，用于不同的目的。例如，你可以在-{}-标记后传递命令行参数给脚本，它们将被存储在CMAKE\_ARGV变量中（CMAKE\_ARGC变量将包含计数）。

让我们介绍其他变量类别，以便我们清楚地了解它们。

\mySubsubsection{2.3.2}{使用环境变量}

这是最简单类型的变量。CMake会复制启动cmake进程时环境中存在的变量，并在单个全局范围内使它们可用。要引用这些变量，请使用\$ENV\{\}语法。

CMake会更改这些变量，但更改仅会应用于正在运行的cmake进程中的本地副本，而不会实际更改系统环境；此外，这些更改对后续构建或测试的运行是不可见的，因此不推荐这样做。

请注意，有几个环境变量会影响CMake行为的不同方面。例如，CXX变量指定将用于编译C++文件的执行文件。我们将介绍环境变量，因为它们与本书相关。完整列表可在文档中找到：\url{https://cmake.org/cmake/help/latest/manual/cmake-envvariables.7.html}。

\begin{myNotic}{Note}
重要的是要意识到，如果您将ENV变量用作命令的参数，则在生成构建系统期间将进行变量插值。这意味着它们将永久性地嵌入到构建树中，因此在构建阶段更改环境将不会有任何效果。
\end{myNotic}

例如，以下是一个项目文件：

\filename{ch02/03-environment/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.20.0)
project(Environment)
message("generated with " $ENV{myenv})
add_custom_target(EchoEnv ALL COMMAND echo "myenv in build
  is" $ENV{myenv})
\end{cmake}

前面的示例有两个步骤：它将在配置期间打印myenv环境变量，并通过add\_custom\_target()添加一个构建阶段，该阶段将作为构建过程的一部分回显相同的变量。我们可以使用一个bash脚本来测试在配置阶段和使用另一个值构建阶段会发生什么：

\filename{ch02/03-environment/build.sh}

\begin{shell}
#!/bin/bash
export myenv=first
echo myenv is now $myenv
cmake -B build .
cd build
export myenv=second
echo myenv is now $myenv
cmake --build .
\end{shell}

运行前面的代码清楚地显示，在配置期间设置的值会保留到生成的构建系统中：

\begin{shell}
$ ./build.sh | grep -v "\-\-"
myenv is now first
generated with first
myenv is now second
Scanning dependencies of target EchoEnv
myenv in build is first
Built target EchoEnv
\end{shell}

至此，我们暂时讨论了环境变量。现在让我们继续讨论最后一类变量：缓存变量。

\mySubsubsection{2.3.3}{使用缓存变量}

我们在第一章《CMake的第一步》讨论cmake的命令行选项时首次提到了缓存变量。本质上，它们是存储在构建树的CMakeCache.txt文件中的持久变量。它们包含项目配置阶段收集的信息。这些信息来源于系统（编译器、链接器、工具等的路径）和用户，通过GUI或使用-D选项从命令行提供。再次强调，缓存变量在脚本中不可用；它们只存在于项目中。

如果\$\{<name>\}引用找不到当前作用域中定义的正常变量，但存在同名的缓存变量，则使用缓存变量。但是，它们也可以通过\$CACHE\{<name>\}语法显式引用，并使用set()命令的特殊形式定义:

\begin{cmake}
set(<variable> <value> CACHE <type> <docstring> [FORCE])
\end{cmake}

与普通变量的set()命令不同，缓存变量需要额外的参数:<type>和<docstring>。这是因为这些变量可以由用户配置，并且GUI需要这些信息来适当地显示它们。

以下类型可以接受:

\begin{itemize}
\item
BOOL: 一个布尔开关值。GUI将显示一个复选框。

\item
FILEPATH:磁盘上一个文件的路径。GUI将打开一个文件对话框。

\item
PATH: 磁盘上一个目录的路径。GUI将打开一个目录对话框。

\item
STRING:一行文字。GUI提供了一个要填充的文本字段。它可以通过调用set\_property(CACHE <variable> STRINGS <values>)来替换为下拉控件。

\item
INTERNAL: 一行文本。GUI将跳过内部条目。内部条目可以用来在运行之间持久存储变量。使用此类型隐式添加FORCE关键字。
\end{itemize}

<doctring>值只是一个标签，GUI将在字段旁边显示它，以便向用户提供有关此设置的更多细节。即使对于内部类型也是必需的。

在代码中设置缓存变量在某种程度上遵循与环境变量相同的规则——值只在当前CMake执行中被覆盖。然而，如果缓存文件中不存在该变量或指定了可选的FORCE参数，该值将被持久化：

\begin{cmake}
set(FOO "BAR" CACHE STRING "interesting value" FORCE)
\end{cmake}

与C++类似，CMake支持变量作用域，尽管是以一种相当特定的方式实现的。

\mySubsubsection{2.3.4}{如何在CMake中正确使用变量作用域}

变量作用域可能是CMake语言中最奇特的概念。这可能是因为我们习惯了它在通用编程语言中的实现方式。我们之所以提前解释这一点，是因为对作用域的错误理解常常是难以发现和修复的bug的来源。

需要明确的是，作为一般概念的变量作用域，旨在用代码分隔不同的抽象层。作用域以树状方式相互嵌套。最外层的作用域（根作用域）称为全局作用域。任何作用域都可以称为局部作用域，以指示当前执行或讨论的作用域。作用域在变量之间创建边界，使得嵌套作用域可以访问在外层作用域中定义的变量，但反之则不行。

CMake有两种变量作用域：

\begin{itemize}
\item
文件：在文件内执行块和自定义函数时使用

\item
目录：在调用add\_subdirectory()命令以在嵌套目录中执行另一个CMakeLists.txt列表文件时使用
\end{itemize}

\begin{myNotic}{Note}
条件块、循环块和宏不会创建独立的作用域。
\end{myNotic}

那么，CMake中变量作用域的实现方式有何不同？当创建嵌套作用域时，CMake简单地用外层作用域中所有变量的副本填充它。随后的命令将影响这些副本。但是一旦嵌套作用域的执行完成，所有副本都会被删除，外层作用域的原始变量将恢复。

作用域在CMake中的工作方式有一些在其他语言中不常见的有趣含义。在嵌套作用域中执行时，如果你取消设置（unset()）在外层作用域中创建的变量，它将消失，但仅在当前的嵌套作用域中，因为变量是局部副本。如果你现在引用这个变量，CMake会确定没有定义这样的变量，它会忽略外层作用域，并继续搜索缓存变量（被认为是独立的）。这是一个可能的问题。

文件变量作用域通过block()和function()命令（但不包括macro()）打开，并分别通过endblock()和endfunction()命令关闭。我们将在本章的命令定义部分介绍函数。现在，让我们通过更简单的block()命令（在CMake 3.25中引入）来看看变量作用域在实践中是如何工作的。

考虑以下示例：

\filename{ch02/04-scope/scope.cmake}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)

set(V 1)
message("> Global: ${V}")
block() # outer block
  message(" > Outer: ${V}")
  set(V 2)
  block() # inner block
    message(" > Inner: ${V}")
    set(V 3)
    message(" < Inner: ${V}")
  endblock()
  message(" < Outer: ${V}")
endblock()
message("< Global: ${V}")
\end{cmake}

我们最初在全局作用域中将变量V设置为1。进入外层和内层块后，我们立即将它们分别更改为2和3。我们还在进入和退出每个作用域时打印变量：

\begin{shell}
> Global: 1
  > Outer: 1
    > Inner: 2
    < Inner: 3
  < Outer: 2
< Global: 1
\end{shell}

如前所述，当我们进入每个嵌套作用域时，变量值从外层作用域暂时复制，但在退出时恢复其原始值。这在输出的最后两行中反映出来。

block()命令还可以将值传播到外层作用域（就像C++默认会做的那样），但这需要通过PROPAGATE关键字显式启用。如果我们为内层块启用传播（block(PROPAGATE V)），输出将如下所示：

\begin{shell}
> Global: 1
  > Outer: 1
    > Inner: 2
    < Inner: 3
  < Outer: 3
< Global: 1
\end{shell}

再次强调，我们影响了外部代码块的范围内，但没有影响到全局范围。

另一种修改外部作用域中的变量的方法是，为 set() 和 unset() 命令设置 PARENT\_SCOPE 标志：

\begin{cmake}
set(MyVariable "New Value" PARENT_SCOPE)
unset(MyVariable PARENT_SCOPE)
\end{cmake}

这种变通方法有一定的局限性，因为它不允许访问超过一级以上的变量。另一点值得注意的是，使用 PARENT\_SCOPE 并不会改变当前作用域中的变量。

现在我们知道了如何处理基本变量，让我们来看一个特殊情况：由于所有变量都作为字符串存储，CMake 必须采取更创造性的方法来处理更复杂的数据结构，比如列表。











































