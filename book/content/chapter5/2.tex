如果曾经使用过GNU Make，一定已经接触过目标（target）这个概念了。本质上，它是一个构建系统遵循的方式，用于将一组文件编译成另一个文件。可以是一个.cpp实现文件编译成一个.o对象文件，或者是一组.o文件打包成一个.a静态库。在构建系统中，目标有着众多的组合和可能性。

然而，CMake在更高的抽象层次上工作，它理解大多数语言如何将源文件构建为可执行文件，可以节省时间并跳过定义这些目标的中间步骤。所以，不需要像使用GNU Make那样编写显式的命令来编译C++对象文件。只需要一个add\_executable()命令，其第一个参数是可执行目标的名字，后续的参数是所有源文件构成的列表：

\begin{cmake}
add_executable(app1 a.cpp b.cpp c.cpp)
\end{cmake}

前面的章节中使用过这个命令，并且已经了解了在实践中，如何使用可执行目标——在生成步骤中，CMake将创建一个构建系统，并用适当的配方填充它，以编译每个源文件，并将它们链接成一个单一的二进制可执行文件。

CMake中，可以使用以下三个命令创建目标：

\begin{itemize}
\item
add\_executable()

\item
add\_library()

\item
add\_custom\_target()
\end{itemize}

构建可执行文件或库之前，CMake会进行一次检查，以确定生成过程中使用的源文件是否有更新，这种机制帮助CMake只重新构建发生变动的工件。通过比较时间戳，CMake有效地识别出哪些目标需要重新构建，从而减少了不必要的编译。

所有创建目标的命令都需要将目标名称作为第一个参数提供，可以在与目标进行交互的其他命令中引用该目标名称，如target\_link\_libraries()，target\_sources()或target\_include\_directories()，稍后会介绍这些命令。

现在，先来看看可以定义哪些目标。

\mySubsubsection{5.2.1.}{定义可执行目标}

定义可执行目标的命令add\_executable()（在前面的章节中已经使用过）：

\begin{shell}
add_executable(<name> [WIN32] [MACOSX_BUNDLE]
                [EXCLUDE_FROM_ALL]
                [source1] [source2 ...])
\end{shell}

如果为Windows编译，通过添加可选参数WIN32关键字，将生成一个不会显示默认控制台窗口（通常在这里看到输出流到std::cout）的可执行文件。相反，应用程序将生成自己的GUI。

下一个可选参数MACOSX\_BUNDLE在某种程度上类似；它使得为macOS/iOS生成的应用程序可以在Finder中打开，作为GUI应用程序启动。

当使用EXCLUDE\_FROM\_ALL关键字时，该目标不会在常规默认构建中构建。这样的目标只有使用下面的命令明确指出要构建时，才会执行构建：

\begin{shell}
cmake --build -t <target>
\end{shell}

最后，需要提供编译目标的源文件列表。支持以下的扩展名：

\begin{itemize}
\item
对于C语言: c, m

\item
对于C++语言: C, M, c++, cc, cpp, cxx, m, mm, mpp, CPP, ixx, cppm, ccm, cxxm, c++m
\end{itemize}

我们没有将头文件添加到源文件列表中。头文件有两种方法指定：使用以头文件所在路径做为参数的target\_include\_directories()命令，或者使用target\_sources()命令的FILE\_SET功能（在CMake 3.23中添加）。这对于可执行文件来说是一个重要的话题，但由于它与目标正交且复杂，我们将在第7章再详细讨论。

\mySubsubsection{5.2.2.}{定义库目标}

定义库与定义可执行文件非常相似，但不需要定义GUI方面的关键字。以下是该命令的签名：

\begin{shell}
add_library(<name> [STATIC | SHARED | MODULE]
            [EXCLUDE_FROM_ALL]
            [<source>...])
\end{shell}

关于名称、排除所有和源的规则与可执行目标完全一致。唯一的区别在于STATIC、SHARED和MODULE关键字。如果有使用库的经验，就知道这些定义了CMake将生成哪种工件：静态链接库、共享（动态库）或模块。这又是一个相当广泛的主题，我们将在第8章中深入讨论。

\mySubsubsection{5.2.3.}{自定义目标}

自定义目标与可执行文件或库略有不同，通过执行明确给出的命令行来扩展构建功能，可用于：

\begin{itemize}
\item
计算其他二进制文件的校验和。

\item
运行sanitizer执行代码扫描并收集结果。

\item
将编译报告发送到指标通道。
\end{itemize}

从这张列表中可以猜到，自定义目标只在相当高级的项目中有用，所以我们只了解基础知识，然后继续讨论更重要的主题。

要定义一个自定义目标，请使用以下语法（为了简洁，省略了一些选项）：

\begin{shell}
add_custom_target(Name [ALL] [COMMAND command2 [args2...] ...])
\end{shell}

自定义目标有一些需要考虑的缺点。由于涉及shell命令，可能是系统特定的，会限制可移植性。此外，自定义目标可能没有为CMake提供直接的方式，来确定生成的具体工件或副产品（如果有的话）。

自定义目标也不像可执行文件和库会进行陈旧性检查（不验证源文件是否比二进制文件更新），因为默认情况下它们不会添加到依赖关系图中（自定义目标在默认构建中不会生成，可以指定ALL关键字，使得每次构建都执行生成自定义目标，ALL与EXCLUDE\_FROM\_ALL具有完全相反的定义）。

来看看这个依赖关系图什么。

\mySubsubsection{5.2.4.}{依赖关系图}

成熟的应用程序通常由许多组件构建而成，特别是内部库。从结构的角度来看，划分项目是有用的。当相关的事物包装在一个逻辑实体中时，就可以与其他目标链接：另一个库或一个可执行文件。这对于多个目标使用同一个库的情况尤其方便。图5.1描述了一个示例依赖关系图：

\myGraphic{0.5}{content/chapter5/images/1.png}{图5.1：BankApp项目中构建依赖的顺序}

这个项目有两个库，两个可执行文件和一个自定义目标。用例是提供一个带有GUI的银行应用程序（GuiApp），以及一个作为自动化脚本一部分使用的命令行版本（TerminalApp）。两个可执行文件都依赖于相同的Calculations库，但只有一个需要Drawing库。为了确保应用程序二进制文件的完整性，我们还将计算一个校验和，并通过单独的安全渠道分发它。CMake在编写此类解决方案的列表文件时非常灵活：

\filename{ch05/01-targets/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(BankApp CXX)

add_executable(terminal_app terminal_app.cpp)
add_executable(gui_app gui_app.cpp)
target_link_libraries(terminal_app calculations)
target_link_libraries(gui_app calculations drawing)

add_library(calculations calculations.cpp)
add_library(drawing drawing.cpp)

add_custom_target(checksum ALL
    COMMAND sh -c "cksum terminal_app>terminal.ck"
    COMMAND sh -c "cksum gui_app>gui.ck"
    BYPRODUCTS terminal.ck gui.ck
    COMMENT "Checking the sums..."
)
\end{cmake}

通过使用target\_link\_libraries()命令将库与可执行文件链接。若不连接，会因未定义的符号，构建可执行文件失败。这里，发现在声明库之前就调用了这个命令吗？当CMake配置项目时，会收集关于目标和属性信息——名称、依赖关系、源文件和其他信息。

解析了所有文件之后，CMake将尝试构建一个依赖关系图。像所有有效的依赖关系图一样，它们是有向无环图（DAG）。所以有一个清晰的哪个目标依赖于哪个目标，并且这样的依赖关系不会形成循环。

当在构建模式下执行cmake时，生成的构建系统将检查定义的顶层目标，并递归地构建依赖关系。来看一下啊图5.1中的例子：

\begin{enumerate}
\item
从顶层开始，构建组1中的两个库。

\item
当Calculations和Drawing库完成后，构建组2 – GuiApp和TerminalApp。

\item
构建校验和目标；运行指定的命令行以生成校验和（cksum是一个Unix校验和工具，这个例子不会在其他平台上构建）。
\end{enumerate}

然而，这里有一个小问题——前面的解决方案不能保证校验和目标会在可执行文件之后构建。CMake不知道校验和依赖于可执行二进制文件的存在，所以它可以自由地首先开始构建它。为了解决这个问题，我们可以在文件末尾放置add\_dependencies()命令：

\begin{cmake}
add_dependencies(checksum terminal_app gui_app)
\end{cmake}

这将确保CMake理解校验和目标与可执行文件之间的关系。

那很好，但target\_link\_libraries()和add\_dependencies()之间有什么区别呢？target\_link\_libraries()旨在与实际库一起使用，并允许你控制属性传播。第二个命令仅用于顶级目标，以设置它们的构建顺序。

随着项目复杂性的增加，依赖树变得难以理解。要如何简化这个过程呢？

\mySubsubsection{5.2.5.}{可视化依赖关系}

即使是小项目，也可能难以推理并与其他开发者分享。一个整洁的图表将大有帮助。毕竟，一图胜千言。我们可以自己动手绘制图表，就像图5.1那样。但这很繁琐，并且每次项目更改时都需要更新。幸运的是，CMake有一个很棒的模块，可以生成dot/graphviz格式的依赖关系图，支持内部和外部依赖！

要使用它，可以简单地执行以下命令：

\begin{shell}
cmake --graphviz=test.dot .
\end{shell}

该模块将生成一个文本文件，可以将其导入到Graphviz可视化软件中，该软件可以渲染图像或生成PDF或SVG文件，可以作为软件文档的一部分存储。每个人都喜欢优秀的文档，但几乎没有人喜欢创建它——现在，不需要了！

自定义目标默认情况下是不可见的，需要创建一个特殊的配置文件，CMakeGraphVizOptions.cmake，就可以自定义图表了。使用set(GRAPHVIZ\_CUSTOM\_TARGETS TRUE)命令在图表中启用自定义目标：

\filename{ch05/01-targets/CMakeGraphVizOptions.cmake}

\begin{cmake}
set(GRAPHVIZ_CUSTOM_TARGETS TRUE)
\end{cmake}

其他选项允许添加图表名称、标题、节点前缀，并配置应包含或排除在输出中的目标（按名称或类型）。访问官方CMake文档以获取CMakeGraphVizOptions模块的完整描述。

甚至可以直接在浏览器中运行Graphviz，地址是：\url{https://dreampuf.github.io/GraphvizOnline/}。

所需要做的就是将test.dot文件的内容复制粘贴到左边的窗口中，项目就会可视化（如图5.2）。非常方便，不是吗？

\myGraphic{0.9}{content/chapter5/images/2.png}{图5.2：在Graphviz中可视化BankApp示例}

使用这种方法，可以快速看到所有明确定义的目标。

现在理解了目标的概念，我们知道如何定义不同类型的目标，包括可执行文件、库和自定义目标，以及如何创建依赖关系图并打印它。让我们使用这些信息进行更深入的研究，看看如何配置它们。

\mySubsubsection{5.2.6.}{设置目标的属性}

目标具有类似于C++对象字段的属性。其中一些属性为了修改而设计的，而有些是只读的。CMake定义了大量“已知属性”（请参阅扩展阅读部分），这些属性取决于目标的类型（可执行文件、库或自定义），也可以添加自己的属性。使用以下命令来操作目标的属性：

\begin{shell}
get_target_property(<var> <target> <property-name>)
set_target_properties(<target1> <target2> ...
                      PROPERTIES <prop1-name> <value1>
                      <prop2-name> <value2> ...)
\end{shell}

要在屏幕上输出目标属性，首先需要将其存储在<var>变量中，然后将其消息传递给用户。读取属性必须逐个进行；设置目标属性允许我们同时为多个目标指定多个属性。

\begin{myNotic}{Note}
属性的概念不仅适用于目标；CMake支持为其他范围设置属性：GLOBAL、DIRECTORY、SOURCE、INSTALL、TEST和CACHE。为了操作所有类型的属性，有通用的get\_property()和set\_property()命令。在某些项目中，会看到这些低层命令用来精确地完成set\_target\_properties()命令所做的事情：

\begin{shell}
set_property(TARGET <target> PROPERTY <name> <value>)
\end{shell}
\end{myNotic}

通常，尽可能多地使用高级命令是更好的选择。有时，CMake提供了简写命令。例如，add\_dependencies( )是向MANUALLY\_ADDED\_DEPENDENCIES目标属性追加依赖关系的简写。这种情况下，可以使用get\_target\_property()精确地查询，就像其他属性一样。然而，不能使用set\_target\_properties()来更改MANUALLY\_ADDED\_DEPENDENCIES（是只读的），因为CMake坚持使用add\_dependencies()命令来限制操作仅为追加。

在接下来的章节中讨论编译和链接时，我们将介绍更多设置属性的命令。

现在，让我们专注于一个目标的属性如何传递给另一个目标。

\mySamllsection{传递目标的使用要求}

命名是困难的，有时会得到一个难以理解的标签。不幸的是，在线CMake文档中，会遇到“传递使用要求”这样一个令人费解的标题。让我们解开这个奇怪的名字，并提供一个更容易理解的术语。

从中间的术语开始：使用。正如我们之前讨论的，一个目标可能依赖于另一个。CMake文档有时将这种依赖称为“使用”，即一个目标使用另一个目标。

在某些情况下，使用的目标会为自己设置特定的属性或依赖项，这反过来构成了使用它的其他目标的依赖：链接某些库、包含目录或要求特定的编译器特性。

解谜的最后一部分，“传递”这个词描述的行为正确（也许可以更简单一些）。CMake将一些使用目标的属性/要求添加到使用目标的属性中。

所以，一些属性可以隐式地在目标之间传递（或简单地传播），因此更容易表达依赖关系。

简化这个整个概念，就像源目标（被使用的目标）和目标目标（使用其他目标的目标）之间的传播属性。

来看一个具体的例子，来理解其为什么存在，以及是如何工作的：

\begin{shell}
target_compile_definitions(<source> <INTERFACE|PUBLIC|PRIVATE> [items1...])
\end{shell}

这个目标命令将填充一个<source>目标的COMPILE\_DEFINITIONS属性。编译定义就是传递给编译器的-Dname=definition标志，用于配置C++预处理器定义（将在第7章中讨论）。这里有趣的部分是第二个参数，需要指定三个值中的一个，INTERFACE、PUBLIC或PRIVATE，以控制属性应该传递给哪个目标。现在，不要将这些与C++访问修饰符混淆——这是一个全新的概念。

传播关键字的工作方式如下：

\begin{itemize}
\item
PRIVATE设置源目标的属性。

\item
INTERFACE设置使用目标的目标属性。

\item
PUBLIC设置源目标和使用目标的目标属性。
\end{itemize}

当属性不应传递给其他目标时，将其设置为PRIVATE。当需要这样的传递时，选择PUBLIC。如果处于一个源目标在其实现（.cpp文件）中就不使用该属性，而在头文件中使用，并且这些属性传递给使用目标的目标，则应使用INTERFACE关键字。

这是如何工作的？为了管理这些属性，CMake提供了一些命令，例如之前提到的target\_compile\_definitions()。当指定PRIVATE或PUBLIC关键字时，CMake将在目标的属性中存储提供的值，COMPILE\_DEFINITIONS。此外，关键字是INTERFACE或PUBLIC，将在具有INTERFACE\_前缀的属性中存储值——INTERFACE\_COMPILE\_DEFINITIONS。配置阶段，CMake将读取源目标的接口属性，并将其内容附加到目标目标。就这样传播属性，或CMake所说的传递目标的使用要求。

使用set\_target\_properties()命令管理的属性可以在\url{https://cmake.org/cmake/help/latest/manual/cmake-properties.7.html}找到，在目标属性部分（并非所有目标属性都可传递）。这里是最重要的几个：

\begin{itemize}
\item
COMPILE\_DEFINITIONS

\item
COMPILE\_FEATURES

\item
COMPILE\_OPTIONS

\item
INCLUDE\_DIRECTORIES

\item
LINK\_DEPENDS

\item
LINK\_DIRECTORIES

\item
LINK\_LIBRARIES

\item
LINK\_OPTIONS

\item
POSITION\_INDEPENDENT\_CODE

\item
PRECOMPILE\_HEADERS

\item
SOURCES
\end{itemize}

我们将在接下来的页面中讨论大多数这些选项，但所有这些选项当然都在CMake手册中有详细描述。在以下链接中找到它们的详细描述：\url{https://cmake.org/cmake/help/latest/prop_tgt/.html}

接下来的问题是，如何远传播这些属性。属性只设置在第一个目标上，还是发送到依赖图的最顶层？这可以决定。

为了在目标之间创建依赖关系，使用target\_link\_libraries()命令。这个命令需要一个传播关键字：

\begin{shell}
target_link_libraries(<target>
                    <PRIVATE|PUBLIC|INTERFACE> <item>...
                    [<PRIVATE|PUBLIC|INTERFACE> <item>...]...)
\end{shell}

这个签名也指定了传播关键字，控制源目标的属性如何在目标目标中存储。图5.3展示了生成阶段（配置阶段完成后）传播属性会发生什么情况：

\myGraphic{0.9}{content/chapter5/images/3.png}{图5.3：属性是如何在目标间进行传递的}

传播关键字的工作方式：

\begin{itemize}
\item
PRIVATE将源值添加到源目标的私有属性。

\item
INTERFACE将源值添加到源目标的接口属性。

\item
PUBLIC将值添加到源目标的两个属性。
\end{itemize}

INTERFACE属性仅用于将属性进一步传播到链中的下一个目标，而源目标在其构建过程中不会使用。

我们之前使用的基本target\_link\_libraries(…)命令隐式指定了PUBLIC关键字。

如果正确地为源目标设置了传播关键字，属性将自动放置在目标目标上——除非有冲突…

\mySamllsection{处理冲突的传播属性}

当一个目标依赖于多个其他目标时，可能存在传播属性之间直接冲突的情况。例如，一个使用的目标将POSITION\_INDEPENDENT\_CODE属性设置为true，而另一个设置为false。CMake将这种冲突理解为错误，并打印出类似于这样的错误信息：

\begin{shell}
CMake Error: The INTERFACE_POSITION_INDEPENDENT_CODE property of "source_ target" does not agree with the value of POSITION_INDEPENDENT_CODE already determined for "destination_target".
\end{shell}

开发者需要明确知道这种冲突，并且需要去解决它。CMake有一些自己的属性，这些属性必须在源目标和目标目标之间“一致”。

有时，这可能很重要——例如，在多个目标中使用相同的库，然后将它们链接到一个可执行文件。如果这些源目标使用的是同一库的不同版本，可能会遇到问题。

为了确保只使用特定版本的库，可以创建一个自定义接口属性，INTERFACE\_LIB\_VERSION，并在其中存储版本。这还不够解决问题，因为CMake默认不会传播自定义属性（这个机制只适用于内置目标属性），必须明确地将自定义属性添加到“兼容”属性列表中。

每个目标都有四个这样的列表：

\begin{itemize}
\item
COMPATIBLE\_INTERFACE\_BOOL

\item
COMPATIBLE\_INTERFACE\_STRING

\item
COMPATIBLE\_INTERFACE\_NUMBER\_MAX

\item
COMPATIBLE\_INTERFACE\_NUMBER\_MIN
\end{itemize}

将属性添加到它们中的任何一个，都会触发传播和兼容性检查。BOOL列表将检查所有传递到目标目标的属性是否评估为相同的布尔值。类似地，STRING将评估为字符串。NUMBER\_MAX和NUMBER\_MIN略有不同——传递的值不必匹配，但目标目标将只接收最高或最低值。

这个例子将帮助我们了解，如何在实践中对其进行应用：

\filename{ch05/02-propagated/CMakeLists.txt}

\begin{cmake}
cmake_minimum_required(VERSION 3.26)
project(PropagatedProperties CXX)

add_library(source1 empty.cpp)
set_property(TARGET source1 PROPERTY INTERFACE_LIB_VERSION 4)
set_property(TARGET source1 APPEND PROPERTY
             COMPATIBLE_INTERFACE_STRING LIB_VERSION)

add_library(source2 empty.cpp)
set_property(TARGET source2 PROPERTY INTERFACE_LIB_VERSION 4)

add_library(destination empty.cpp)

target_link_libraries(destination source1 source2)
\end{cmake}

这里，创建了三个目标；为了简化，所有目标都使用相同的空源文件。在两个源目标上，指定了带有INTERFACE\_前缀的自定义属性，并将其设置为相同的匹配库版本。两个源目标都链接到相应的目标。最后，我们指定了字符串兼容性作为source1的属性（这里没有添加INTERFACE\_前缀，因为使用target\_link\_libraries链接库文件时，会将类似INTERFACE\_LIB\_VERSION的属性值追加到destination的LIB\_VERSION属性）。

CMake将这个自定义属性传播到相应目标，并检查所有源目标的版本是否完全匹配（兼容性属性只需在目标目标上设置一次）。

已经了解了常规目标是什么，那就来看看其他像目标、闻起来像目标，有时甚至表现得像目标的东西，但最终发现它们并不是真正的目标。

\mySubsubsection{5.2.7.}{识别伪目标}

目标包含的一些概念非常有用，CMake也将这些特性应用在了如外部依赖项、别名等伪目标上。创建目标会在构建系统中生成对应的工件，如可执行文件、库文件等。但是创建的伪目标在构建系统中不会生成具体的工件，它可以用来作为生成目标时的依赖或输入：

\begin{itemize}
\item
导入的目标

\item
别名目标

\item
接口库
\end{itemize}

\mySamllsection{导入的目标}

如果浏览了本书的目录，会了解将讨论CMake如何管理外部依赖项——其他项目、库等。IMPORTED目标是这个过程的产物。CMake可以定义它们，作为find\_package()命令的结果。

可以调整这类目标的属性：编译定义、编译选项、包含目录等——甚至支持目标传递的使用要求。然而，应该将它们视为不可变的目标；不要更改它们的源文件或依赖关系。

IMPORTED目标的定义范围可以是全局的，也可以是定义它们的目录的本地范围（在子目录中可见，但在父目录中不可见）。

\mySamllsection{别名目标}

别名目标的确切作用就是你所期望的——为目标创建另一个不同的名称引用。可以为可执行文件和库创建别名目标：

\begin{shell}
add_executable(<name> ALIAS <target>)
add_library(<name> ALIAS <target>)
\end{shell}

别名目标的属性只读，不能安装或导出别名（在生成的构建系统中不可见）。

为什么还要有别名呢？有时，它们非常有用，比如项目的一部分（如子目录）需要以特定名称引用一个目标，而实际的实现可能因情况而异。例如，希望根据用户的选择构建解决方案中的库或导入它。

\mySamllsection{接口库}

这是一个有趣的构造——一个不编译东西的库，而是作为一个实用目标。整个概念都围绕传播属性（传递使用要求）构建。

接口库主要有两个用途——代表仅包含头文件的库，以及将一堆传播属性打包成一个逻辑单元。

仅包含头文件的库可以用add\_library(INTERFACE)轻松创建：

\begin{cmake}
add_library(Eigen INTERFACE
    src/eigen.h src/vector.h src/matrix.h
)
target_include_directories(Eigen INTERFACE
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
    $<INSTALL_INTERFACE:include/Eigen>
)
\end{cmake}

在前面的代码片段中，创建了一个包含三个头文件的Eigen接口库。接下来，使用生成器表达式（这些以美元符号和尖括号表示，\$<…>，将在下一章解释），将包含目录设置为当目标导出时为\$\{CMAKE\_CURRENT\_SOURCE\_DIR\}/src，安装时为include/Eigen（这将在本章的末尾解释）。

要使用这样的库，只需链接它即可：

\begin{cmake}
target_link_libraries(executable Eigen)
\end{cmake}

这里不会发生实际的链接，但CMake会将这个命令理解为，向可执行目标传播所有INTERFACE属性的请求。

第二种使用场景利用了相同的机制，但出于不同的目的——创建了一个逻辑目标，可以作为一个传播属性的占位符。然后，可以将这个目标用作其他目标的依赖，并以干净、方便的方式设置属性。这是一个例子：

\begin{cmake}
add_library(warning_properties INTERFACE)
target_compile_options(warning_properties INTERFACE
    -Wall -Wextra -Wpedantic
)
target_link_libraries(executable warning_properties)
\end{cmake}

add\_library(INTERFACE)命令创建了一个逻辑的warning\_properties目标，用于在第二个命令中为可执行目标设置编译选项。我建议使用这些INTERFACE目标，它们可以提高代码的可读性和可重用性，这是将一堆魔法值重构为命名良好的变量的过程。我还建议明确地为接口库添加一个后缀，如\_properties，以便轻松区分接口库和常规库。

\mySubsubsection{5.2.8.}{对象库}

对象库用于将多个源文件，组合成一个单一的逻辑目标，并在构建过程中将它们编译成（.o）对象文件。要创建一个对象库，遵循与创建其他库相同的方法，但使用OBJECT关键字：

\begin{shell}
add_library(<target> OBJECT <sources>)
\end{shell}

构建过程中产生的对象文件可以作为其他目标的编译元素，使用\$<TARGET\_OBJECTS:objlib>生成器表达式：

\begin{shell}
add_library(... $<TARGET_OBJECTS:objlib> ...)
add_executable(... $<TARGET_OBJECTS:objlib> ...)
\end{shell}

或者，可以使用target\_link\_libraries()命令将它们作为依赖项进行添加。

在Calc库的上下文中，对象库将非常有用，以避免为库的静态和共享版本编译库源的冗余。对于共享库，明确地编译具有POSITION\_INDEPENDENT\_CODE启用的对象文件是必要的。

回到项目的目标：calc\_obj将提供编译后的对象文件，然后将用于calc\_static和calc\_shared库。让我们探索这两种类型库之间的实际区别，并理解为什么可能需要创建两者。

伪目标是否穷尽了目标的概念？当然不是！我们仍然需要了解，这些目标是如何用于生成构建系统的。

\mySubsubsection{5.2.9.}{构建目标}

项目的上下文和生成的构建系统中，“目标”一词可以有不同的含义。在生成构建系统的上下文中，CMake将CMake语言编写的列表文件“编译”成所选构建工具的语言，例如为GNU Make创建Makefile。这些生成的Makefile有自己的目标集。其中一些目标是列表文件中定义的目标的直接转换，而其他目标则在构建系统生成过程中隐式创建。

CMake默认为顶级列表文件的所有目标执行构建，如可执行文件和库（自定义目标根据标志选择是否构建），这种目标构建策略为ALL。当运行cmake -{}-build <build tree>时，ALL将构建所有顶级列表文件目标。第一章中，可以通过添加-{}-target <name>参数到cmake构建命令中来选择一个目标。

一些可执行文件或库可能不需要在每次构建中都存在，但我们希望将它们作为项目的一部分，以便在不常需要的场合中使用。为了优化默认构建，可以将它们从ALL目标中排除：

\begin{shell}
add_executable(<name> EXCLUDE_FROM_ALL [<source>...])
add_library(<name> EXCLUDE_FROM_ALL [<source>...])
\end{shell}

自定义目标则相反——默认情况下，它们排除在ALL目标之外，除非明确地使用ALL关键字添加它们，就像我们在BankApp示例中所做的那样。

另一个隐式定义的构建目标是clean，简单地从构建树中删除产生的工件。我们使用它来清除所有旧文件并从头开始构建。重要的是要理解，clean并不会简单地删除构建目录中的所有内容。为了正确工作，需要手动指定自定义目标可能创建的文件作为BYPRODUCTS（请参阅BankApp示例）。

我们对目标和它们不同方面的探索之旅结束了！了解了如何创建它们，配置它们的属性，使用伪目标，并决定它们是否应该默认构建。还有一个有趣的非目标机制，可以创建在所有实际目标中使用的自定义工件——自定义命令（不要与自定义目标混淆）。

